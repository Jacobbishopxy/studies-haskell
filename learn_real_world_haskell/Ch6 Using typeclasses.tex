\documentclass[./main.tex]{subfiles}

\begin{document}

Typeclasses 是 Haskell 中最强大的特性。它们允许我们定义通用性的接口，为各种类型提供公共特性集。Typeclasses 是一些语言特性的核心，
例如相等性测试和数字运算符。

\subsection*{对 Typeclasses 的需求}

假设在没有相等性测试\acode{==}的情况下需要构建一个简单的\acode{color}类型，那么相等测试就应该如下：

\begin{lstlisting}[language=Haskell]
  data Color = Red | Green | Blue

  colorEq :: Color -> Color -> Bool
  colorEq Red Red = True
  colorEq Green Green = True
  colorEq Blue Blue = True
  colorEq _ _ = False
\end{lstlisting}

现在假设我们想为\acode{StringS}增加一个相等性测试。由于 Haskell 的\acode{String}是字符列表，我们可以编写一个简单的函数用于测试。
这里为了简化我们使用\acode{==}操作符用于说明。

\begin{lstlisting}[language=Haskell]
  stringEq :: [Char] -> [Char] -> Bool
  stringEq [] [] = True
  stringEq (x:xs) (y:ys) = x == y && stringEq xs ys
  stringEq _ _ = False
\end{lstlisting}

现在已经发现问题了：我们必须为每个不同的类型使用不同名称的比较函数，这是很低效且令人讨厌的。因此需要一个通用的函数可用于比较任何东西。
此外，当新的数据类型之后被创建时，已经存在的代码不能被改变。

Haskell 的 typeclasses 就是设计用来解决上述问题的。

\subsection*{什么是 typeclasses}

Typeclasses 定义了一系列的函数，它们可以根据给定的数据类型有不同的实现。

首先我们必须定义 typeclass 本身。我们希望一个函数接受同样类型的两个参数，返回一个\acode{Bool}来表示它们是否相等。我们无需在意类型
是什么，但需要两个参数的类型相同。下面是 typeclass 的第一个定义：

\begin{lstlisting}[language=Haskell]
  class BasicEq a where
    isEqual :: a -> a -> Bool
\end{lstlisting}

通过\textbf{ghci}的类型检查\acode{:type}可以得知\acode{isEqual}的类型：

\begin{lstlisting}[language=Haskell]
  ghci> :type isEqual
  isEqual :: (BasicEq a) => a -> a -> Bool
\end{lstlisting}

现在可以为特定类型定义\acode{isEqual}：

\begin{lstlisting}[language=Haskell]
  instance BasicEq Bool where
    isEqual True True = True
    isEqual False False = True
    isEqual _ _ = False
\end{lstlisting}

测试：

\begin{lstlisting}[language=Haskell]
  ghci> isEqual False False
  True
  ghci> isEqual False True
  False
  ghci> isEqual "Hi" "Hi"

  <interactive>:1:0:
      No instance for (BasicEq [Char])
        arising from a use of `isEqual' at <interactive>:1:0-16
      Possible fix: add an instance declaration for (BasicEq [Char])
      In the expression: isEqual "Hi" "Hi"
      In the definition of `it': it = isEqual "Hi" "Hi"
\end{lstlisting}

注意在尝试比较两个字符串时，\textbf{ghci}发现我们并未给\acode{String}提供\acode{BasicEq}的实例。因此\acode{ghci}并不知道该
如何对\acode{String}进行比较，同时提议我们可以通过为\acode{[Char]}定义\acode{BasicEq}实例来解决这个问题。

下面是定义一个包含了两个函数的 typeclass：

\begin{lstlisting}[language=Haskell]
  class BasicEq2 a where
    isEqual2 :: a -> a -> Bool
    isNotEqual2 :: a -> a -> Bool
\end{lstlisting}

虽然\acode{BasicEq2}的定义没有问题，但是它让我们做了额外的事情。就逻辑而言，如果我们知道了\acode{isEqual}或\acode{isNotEqual}
中的一个，我们便知道了另一个。那么与其让用户定义 typeclass 中两个函数，我们可以提供一个默认的实践。

\begin{lstlisting}[language=Haskell]
  class BasicEq3 a where
    isEqual3 :: a -> a -> Bool
    isEqual3 x y = not (isNotEqual3 x y)

    isNotEqual3 :: a -> a -> Bool
    isNotEqual3 x y = not (isEqual3 x y)
\end{lstlisting}

\subsection*{声明 typeclass 实例}

现在知道了如何定义 typeclasses，接下来就是直到如何定义 typeclasses 的实例。回忆一下，类型是由一个个特定 typeclass 所构成的实例
所赋予了意义，这些 typeclasses 又实现了必要的函数。

之前为我们的\acode{Color}类型创建了相等性测试，现在让我们试试让其成为\acode{BasicEq3}的实例：

\begin{lstlisting}[language=Haskell]
  instance BasicEq3 Color where
    isEqual3 Red Red = True
    isEqual3 Green Green = True
    isEqual3 Blue Blue = True
    isEqual3 _ _ = False
\end{lstlisting}

注意这里提供了与之前定义的一样的函数，实际上实现是完全相同的。不过在这种情况下，我们可以使用\acode{isEqual3}在\textit{任何}定义了
\acode{BasicEq3}实例的类型上。

另外注意\acode{BasicEq3}定义了\acode{isEqual3}与\acode{isNotEqual3}，而我们仅实现了它们其中一个。这是因为\acode{BasicEq3}
包含了默认实现，因此没有显式定义\acode{isNotEqual3}时，编译器自动的使用了\acode{BasicEq3}中的默认实现。

\subsection*{重要的内建 Typeclasses}

\subsubsection*{Show}

\acode{Show} typeclass 用于将值转换为\acode{String}。可能最常用于将数字转换为字符串，很多类型都有它的实例，因此还可以用于转换
更多的类型。如果自定义类型实现了\acode{Show}的实例，那么就可以在\textbf{ghci}上展示或者在程序中打印出来。

\acode{Show}中最重要的函数就是\acode{show}，它接受一个参数：用于转换的数据，返回一个\acode{String}来表示该数据。

\begin{lstlisting}[language=Haskell]
  ghci> :type show
  show :: (Show a) => a -> String
\end{lstlisting}

一些其它的例子：

\begin{lstlisting}[language=Haskell]
  ghci> show 1
  "1"
  ghci> show [1, 2, 3]
  "[1,2,3]"
  ghci> show (1, 2)
  "(1,2)"
\end{lstlisting}

\textbf{ghci}展示的结果与输入到 Haskell 程序中的结果相同。表达式\acode{show 1}返回的是单个字符的字符串，其包含数字\acode{1}，
也就是说引号并不是字符串本身。通过\acode{putStrLn}可以更清楚的看到：

\begin{lstlisting}[language=Haskell]
  ghci> putStrLn (show 1)
  1
  ghci> putStrLn (show [1,2,3])
  [1,2,3]
\end{lstlisting}

我们也可以使用\acode{show}在字符串上：

\begin{lstlisting}[language=Haskell]
  ghci> show "Hello!"
  "\"Hello!\""
  ghci> putStrLn (show "Hello!")
  "Hello!"
  ghci> show ['H', 'i']
  "\"Hi\""
  ghci> putStrLn (show "Hi")
  "Hi"
  ghci> show "Hi, \"Jane\""
  "\"Hi, \\\"Jane\\\"\""
  ghci> putStrLn (show "Hi, \"Jane\"")
  "Hi, \"Jane\""
\end{lstlisting}

现在为我们自己的类型实现\acode{Show}的实例：

\begin{lstlisting}[language=Haskell]
  instance Show Color where
    show Red = "Red"
    show Green = "Green"
    show Blue = "Blue"
\end{lstlisting}

\subsubsection*{Read}

\acode{Read} typeclass 基本就是相反的\acode{Show}：它定义了函数接受一个\acode{String}，分析它，并返回属于\acode{Read}
成员的任何类型的数据。

\begin{lstlisting}[language=Haskell]
  ghci> :type read
  read :: (Read a) => String -> a
\end{lstlisting}

以下是一个\acode{read}与\acode{show}的例子：

\begin{lstlisting}[language=Haskell]
  main = do
    putStrLn "Please enter a Double:"
    inpStr <- getLine
    let inpDouble = read inpStr :: Double
    putStrLn $ "Twice" ++ show inpDouble ++ " is " ++ show (inpDouble * 2)
\end{lstlisting}

\acode{read}的类型：\acode{(Read a) => String -> a}。这里的\acode{a}是每个\acode{Read}实例的类型。也就是说特定的解析
函数是根据预期的\acode{read}返回类型所决定的。

\begin{lstlisting}[language=Haskell]
  ghci> (read "5.0")::Double
  5.0
  ghci> (read "5.0")::Integer
  *** Exception: Prelude.read: no parse
\end{lstlisting}

在尝试解析\acode{5.0}为\acode{Integer}时异常。当预期返回值的类型是\acode{Integer}时，\acode{Integer}的解析函数并不接受
小数，因此异常被抛出。

\acode{Read} 提供了一些相当复杂的解析器。你可以通过实现\acode{readsPrec}函数定义一个简单的解析。该实现在解析成功时，返回只
包含一个元组的列表，如果解析失败则返回空列表。下面是一个实现\acode{Color}的\acode{Read}实例的例子：

\begin{lstlisting}[language=Haskell]
  instance Read Color where
  -- readsPrec is the main function for parsing input
  readsPrec _ value =
    -- We pass tryParse a list of pairs. Each pair has a string
    -- and the desired return value. tryParse will try to match
    -- the input to one of these strings
    tryParse [("Red", Red), ("Green", Green), ("Blue", Blue)]
    where
      -- If there is nothing left to try, fail
      tryParse [] = []
      tryParse ((attempt, result) : xs) =
        -- Compare the start of the string to be parsed to the
        -- text we are looking for.
        if take (length attempt) value == attempt
          then -- If we have a match, return the result and the remaining input
            [(result, drop (length attempt) value)]
          else -- If we don't have a match, try the next pair in the list of attempts.
            tryParse xs
\end{lstlisting}

测试：

\begin{lstlisting}[language=Haskell]
  ghci> (read "Red")::Color
  Red
  ghci> (read "Green")::Color
  Green
  ghci> (read "Blue")::Color
  Blue
  ghci> (read "[Red]")::[Color]
  [Red]
  ghci> (read "[Red,Red,Blue]")::[Color]
  [Red,Red,Blue]
  ghci> (read "[Red, Red, Blue]")::[Color]
  *** Exception: Prelude.read: no parse
\end{lstlisting}

注意最后一个例子的异常。这是因为我们的解析器并没有聪明到能处理空格。

\begin{anote}
  Read 并没有大范围的被使用

  虽然可以使用\acode{Read} typeclass 来构建复杂的解析器，但许多人发现使用 Parsec 会更容易，且仅依赖\acode{Read}来处理简单的
  任务。在第 16 章会详细介绍 Parsec。
\end{anote}

\subsubsection*{通过 Read 和 Show 进行序列化}

我们经常需要存储一个内存中的数据结构至硬盘供未来使用或者通过网络发送出去，那么将内存中数据转换为一个平坦的字节序列用于存储的这个过程
就被称为\textit{序列化 serialization}。

\begin{anote}
  解析大型字符串

  在 Haskell 中字符串的处理通常都是惰性的，因此\acode{read}与\acode{show}可以被用于处理很大的数据结构而不发生异常。Haskell
  内建的\acode{read}与\acode{show}实例是高效的，并且都是纯 Haskell。如何处理解析异常的更多细节将会在第 19 章中详细介绍。
\end{anote}

测试：

\begin{lstlisting}[language=Haskell]
  ghci> let d1 = [Just 5, Nothing, Nothing, Just 8, Just 9]::[Maybe Int]
  ghci> putStrLn (show d1)
  [Just 5,Nothing,Nothing,Just 8,Just 9]
  ghci> writeFile "test" (show d1)
\end{lstlisting}

再是反序列：

\begin{lstlisting}[language=Haskell]
  ghci> input <- readFile "test"
  "[Just 5,Nothing,Nothing,Just 8,Just 9]"
  ghci> let d2 = read input

  <interactive>:1:9:
      Ambiguous type variable `a' in the constraint:
        `Read a' arising from a use of `read' at <interactive>:1:9-18
      Probable fix: add a type signature that fixes these type variable(s)
  ghci> let d2 = (read input)::[Maybe Int]
  ghci> print d1
  [Just 5,Nothing,Nothing,Just 8,Just 9]
  ghci> print d2
  [Just 5,Nothing,Nothing,Just 8,Just 9]
  ghci> d1 == d2
  True
\end{lstlisting}

这里解释器并不知道\acode{d2}的类型，因此抛出了异常。

以下是一些稍微复杂点的数据结构：

\begin{lstlisting}[language=Haskell]
  ghci> putStrLn $ show [("hi", 1), ("there", 3)]
  [("hi",1),("there",3)]
  ghci> putStrLn $ show [[1, 2, 3], [], [4, 0, 1], [], [503]]
  [[1,2,3],[],[4,0,1],[],[503]]
  ghci> putStrLn $ show [Left 5, Right "three", Left 0, Right "nine"]
  [Left 5,Right "three",Left 0,Right "nine"]
  ghci> putStrLn $ show [Left 0, Right [1, 2, 3], Left 5, Right []]
  [Left 0,Right [1,2,3],Left 5,Right []]
\end{lstlisting}

\newpage
\subsubsection*{数值类型}

Haskell 拥有强大的数值类型。

\begin{center}
  \begin{tabular}{|l|l|}
    \hline
    \multicolumn{2}{|c|}{\textbf{选定的数值类型}}        \\
    \hline
    类型       & 描述                                 \\
    \hline
    Double   & 双精度浮点数。浮点数的通常选择。                   \\
    Float    & 单精度浮点数。通常用于与 C 交互。                 \\
    Int      & 带方向的确定精度整数；最小范围 $[-2^29..2^29-1]$。 \\
    Int8     & 8-bit 带方向的整数                       \\
    Int16    & 16-bit 带方向的整数                      \\
    Int32    & 32-bit 带方向的整数                      \\
    Int64    & 64-bit 带方向的整数                      \\
    Integer  & 带方向的任意精度整数；范围仅受机器限制。很常用。           \\
    Rational & 带方向的任意精度的有理数。以两个 Integers 进行存储。    \\
    Word     & 无方向的确定精度整数；存储大小与 Int 一致            \\
    Word8    & 8-bit 无方向的整数                       \\
    Word16   & 16-bit 无方向的整数                      \\
    Word32   & 32-bit 无方向的整数                      \\
    Word64   & 64-bit 无方向的整数                      \\
    \hline
  \end{tabular}
\end{center}

有很多不同的数值类型。有些运算，比如加法对所有类型都适用；还有其它类型的计算例如\acode{asin}，仅适用于浮点类型。

\begin{center}
  \begin{tabular}{|l|p{6cm}|l|p{3cm}|}
    \hline
    \multicolumn{4}{|c|}{\textbf{选定的数值函数与常量}}                                                             \\
    \hline
    项              & 类型                                        & 模块         & 描述                          \\
    \hline
    (*)            & Num a => a -> a -> a                      & Prelude    & 加法                          \\
    (-)            & Num a => a -> a -> a                      & Prelude    & 减法                          \\
    (*)            & Num a => a -> a -> a                      & Prelude    & 乘法                          \\
    (/)            & Fractional a => a -> a -> a               & Prelude    & 除法                          \\
    (**)           & Floating a => a -> a -> a                 & Prelude    & 幂                           \\
    (\^)           & (Num a, Integral b) => a -> b -> a        & Prelude    & 非负数的幂                       \\
    (^^)           & (Fractional a, Integral b) => a -> b -> a & Prelude    & 分数的幂                        \\
    (\%)           & Integral a => a -> a -> Ratio a           & Data.Ratio & 比例                          \\
    (.\&.)         & Bits a => a -> a -> a                     & Data.Bits  & Bitwise 和                   \\
    (.|.)          & Bits a => a -> a -> a                     & Data.Bits  & Bitwise 或                   \\
    abs            & Num a => a -> a                           & Prelude    & 绝对值                         \\
    approxRational & RealFrac a => a -> a -> Rational          & Data.Ratio & 基于分数分子与分母的近似有理组合            \\
    cos            & Floating a => a -> a                      & Prelude    & Cosine，还有 acos，cosh，与 acosh \\
    div            & Integral a => a -> a -> a                 & Prelude    & 整数除法，总是向下取整，quot 同理         \\
    fromInteger    & Num a => Integer -> a                     & Prelude    & 从 Integer 类型转换为任意数值类型       \\
    fromIntegral   & (Integral a, Num b) => a -> b             & Prelude    & 比上述更泛化的转换                   \\
    fromRational   & Fractional a => Rational -> a             & Prelude    & 从 Rational 转换，可能有损          \\
    log            & Floating a => a -> a                      & Prelude    & 自然 log                      \\
    logBase        & Floating a => a -> a -> a                 & Prelude    & 显式底的 log                    \\
    maxBound       & Bounded a => a                            & Prelude    & bound 类型的最大值                \\
    minBound       & Bounded a => a                            & Prelude    & bound 类型的最小值                \\
    mod            & Integral a => a -> a -> a                 & Prelude    & 整数模                         \\
    pi             & Floating a => a                           & Prelude    & 数学常数的派                      \\
    quot           & Integral a => a -> a -> a                 & Prelude    & 整数除法；商的小数部分向零截断             \\
    \hline
  \end{tabular}
\end{center}

接上表

\begin{center}
  \begin{tabular}{|l|p{6cm}|l|p{3cm}|}
    \hline
    项          & 类型                                 & 模块        & 描述                          \\
    \hline
    recip      & Fractional a => a -> a             & Prelude   & 倒数                          \\
    rem        & Integral a => a -> a -> a          & Prelude   & 整数除法的余数                     \\
    round      & (RealFrac a, Integral b) => a -> b & Prelude   & 向最接近的整数方向取整                 \\
    shift      & Bits a => a -> Int -> a            & Bits      & 左移指定 bits，右移可能为负            \\
    sin        & Floating a => a -> a               & Prelude   & Sine，另 asin，sinh，与 asinh    \\
    sqrt       & Floating a => a -> a               & Prelude   & 平方根                         \\
    tan        & Floating a => a -> a               & Prelude   & Tangent，令 atan，tanh，与 atanh \\
    toInteger  & Integral a => a -> Integer         & Prelude   & 转换任意 Integral 至一个 Integer   \\
    toRational & Real a => a -> Rational            & Prelude   & 转换 Real 至 Rational          \\
    truncate   & (RealFrac a, Integral b) => a -> b & Prelude   & 向零截断数值                      \\
    xor        & Bits a => a -> a -> a              & Data.Bits & Bitwise 的异或                 \\
    \hline
  \end{tabular}
\end{center}

\newblock

\begin{center}
  \begin{tabular}{|p{1.8cm}|l|l||l|l|l||l|l|l|}
    \hline
    \multicolumn{9}{|c|}{\textbf{数值类型的 Typeclass 实例}}                                                \\
    \hline
    项                     & Bits & Bounded & Floating & Fractional & Integral & Num & Rea & RealFrac \\
    \hline
    Double                &      &         & X        & X          &          & X   & X   & X        \\
    Float                 & X    & X       &          &            & X        & X   & X   &          \\
    Int                   & X    & X       &          &            & X        & X   & X   &          \\
    Int16                 & X    & X       &          &            & X        & X   & X   &          \\
    Int32                 & X    & X       &          &            & X        & X   & X   &          \\
    Int64                 & X    & X       &          &            & X        & X   & X   &          \\
    Integer               & X    &         &          &            & X        & X   & X   &          \\
    Rational or any Ratio &      &         &          & X          &          & X   & X   & X        \\
    Word                  & X    & X       &          &            & X        & X   & X   &          \\
    Word16                & X    & X       &          &            & X        & X   & X   &          \\
    Word32                & X    & X       &          &            & X        & X   & X   &          \\
    Word64                & X    & X       &          &            & X        & X   & X   &          \\
    \hline
  \end{tabular}
\end{center}

在数值类型间转换是另一个常用的需求。


\begin{center}
  \begin{tabular}{|l|l|l|l|l|}
    \hline
    \multicolumn{5}{|c|}{\textbf{数值类型之间的转换}}                                                         \\
    \hline
    \textbf{原始类型} & \multicolumn{4}{|c|}{\textbf{目标类型}}                                              \\
    \hline
                  & Double, Float                       & Int, Word    & Integer      & Rational     \\
    \hline
    Double, Float & fromRational . toRational           & truncate *   & truncate *   & toRational   \\
    Int, Word     & fromIntegral                        & fromIntegral & fromIntegral & fromIntegral \\
    Integer       & fromIntegral                        & fromIntegral & N/A          & fromIntegral \\
    Rational      & fromRational                        & truncate *   & truncate *   & N/A          \\
    \hline
  \end{tabular}
\end{center}

\subsection*{自动化派生}

对于很多简单的数据类型，Haskell 编译器可以自动的为我们派生出\acode{Read}，\acode{Show}，\acode{Bounded}，\acode{Enum}，
\acode{Eq}以及\acode{Ord}的实例。这将大大的节省用户编写代码的时间。

\begin{lstlisting}[language=Haskell]
  data Color = Red | Green | Blue
    deriving (Read, Show, Eq, Ord)
\end{lstlisting}

测试：

\begin{lstlisting}[language=Haskell]
  ghci> show Red
  "Red"
  ghci> (read "Red")::Color
  Red
  ghci> (read "[Red,Red,Blue]")::[Color]
  [Red,Red,Blue]
  ghci> (read "[Red, Red, Blue]")::[Color]
  [Red,Red,Blue]
  ghci> Red == Red
  True
  ghci> Red == Blue
  False
  ghci> Data.List.sort [Blue,Green,Blue,Red]
  [Red,Green,Blue,Blue]
  ghci> Red < Blue
  True
\end{lstlisting}

自动派生也不总是能成功。例如如果定义了一个类型\acode{data MyType = MyType (Int -> Bool)}，编译器将不能为其派生\acode{show}
的实例，因为它不知道如何渲染一个函数。这种情况下我们会得到一个编译错误。

当我们自动派生某些 typeclass 的实例时，在数据声明中引用的类型也必须是该 typeclass 的实例（手动或自动的）。

\begin{lstlisting}[language=Haskell]
  data CannotShow = CannotShow
  -- deriving (Show)

  -- will not compile, since CannotShow is not an instance of Show
  data CannotDeriveShow = CannotDeriveShow CannotShow
            deriving (Show)

  data OK = OK

  instance Show OK where
  show _ = "OK"

  data ThisWorks = ThisWorks OK
    deriving (Show)
\end{lstlisting}

注意\acode{CannotShow}的\acode{deriving (Show)}是被注释掉的，因此\acode{CannotDeriveShow}才无法派生\acode{Show}。

\subsection*{Typeclasses 实战：令 JSON 使用起来更方便}

上一章讲述的\acode{JValue}处理 JSON 并不容易。例如，下面是一个实际 JSON 数据的删减整理的片段：

\begin{lstlisting}
  {
    "query": "awkward squad haskell",
    "estimatedCount": 3920,
    "moreResults": true,
    "results":
    [{
      "title": "Simon Peyton Jones: papers",
      "snippet": "Tackling the awkward squad: monadic input/output ...",
      "url": "http://research.microsoft.com/~simonpj/papers/marktoberdorf/",
     },
     {
      "title": "Haskell for C Programmers | Lambda the Ultimate",
      "snippet": "... the best job of all the tutorials I've read ...",
      "url": "http://lambda-the-ultimate.org/node/724",
     }]
  }
\end{lstlisting}

以及在 Haskell 中的表达：

\begin{lstlisting}[language=Haskell]
  import SimpleJSON

  result :: JValue
  result = JObject [
    ("query", JString "awkward squad haskell"),
    ("estimatedCount", JNumber 3920),
    ("moreResults", JBool True),
    ("results", JArray [
       JObject [
        ("title", JString "Simon Peyton Jones: papers"),
        ("snippet", JString "Tackling the awkward ..."),
        ("url", JString "http://.../marktoberdorf/")
       ]])
    ]
\end{lstlisting}

因为 Haskell 并不支持列表中包含不同类型的值，我们不能直接表示一个包含了多个类型的 JSON 对象。我们必须将每个值通过\acode{JValue}
构造函数来进行包装。这限制了我们的灵活性：如果想要更换数值\acode{3920}成为一个字符串\acode{"3,920"}，我们必须更换构造函数，即
\acode{JNumber}变为\acode{JString}。

Haskell 的 typeclasses 为此类问题提供了一个诱人的解决方案：

\begin{lstlisting}[language=Haskell]
  type JSONError = String

  class JSON a where
    toJValue :: a -> JValue
    fromJValue :: JValue -> Either JSONError a

  instance JSON JValue where
    toJValue = id
    fromJValue = Right
\end{lstlisting}

现在无需再将类似\acode{JNumber}这样的构造函数应用在值上将值包裹，直接应用\acode{toJValue}函数在该值上即可。

我们同样提供了一个\acode{fromJValue}函数，用于将一个\acode{JValue}转换成一个我们所期望的类型。

\subsubsection*{更多有帮助的错误}

让我们构造一下自己的\acode{Maybe}与\acode{Either}：

\begin{lstlisting}[language=Haskell]
  data Maybe a
  = Nothing
  | Just a
  deriving (Eq, Ord, Read, Show)

data Either a b
  = Left a
  | Right b
  deriving (Eq, Ord, Read, Show)
\end{lstlisting}

在\acode{Bool}值实例中尝试一下：

\begin{lstlisting}[language=Haskell]
  instance JSON Bool where
    toJValue = JBool
    fromJValue (JBool b) = Right b
    fromJValue _ = Left "not a JSON boolean"
\end{lstlisting}

\subsubsection*{通过类型同义词来创建一个实例}

\begin{lstlisting}[language=Haskell]
  instance JSON String where
    toJValue = JString
    fromJValue (JString s) = Right s
    fromJValue _ = Left "not a JSON string"
\end{lstlisting}

\subsection*{活在开放世界}

Haskell 的 typeclasses 允许我们在任何合适的时候创建新的 typeclass 实例。

\begin{lstlisting}[language=Haskell]
  doubleToJValue :: (Double -> a) -> JValue -> Either JSONError a
  doubleToJValue f (JNumber v) = Right (f v)
  doubleToJValue _ _ = Left "not a JSON number"

  instance JSON Int where
    toJValue = JNumber . realToFrac
    fromJValue = doubleToJValue round

  instance JSON Integer where
    toJValue = JNumber . realToFrac
    fromJValue = doubleToJValue round

  instance JSON Double where
    toJValue = JNumber
    fromJValue = doubleToJValue id
\end{lstlisting}

我们还希望转换一个列表成为 JSON，暂时用\acode{undefined}作为实例的方法。

\begin{lstlisting}[language=Haskell]
  instance (JSON a) => JSON [a] where
    toJValue = undefined
    fromJValue = undefined
\end{lstlisting}

对象亦是如此：

\begin{lstlisting}[language=Haskell]
  instance (JSON a) => JSON [(String, a)] where
    toJValue = undefined
    fromJValue = undefined
\end{lstlisting}

\subsubsection*{合适重叠实例会导致问题}

如果我们将这些定义放入一个原文件中，并加载至\acode{ghci}，每个初始化看起来都没问题：

\begin{lstlisting}[language=Haskell]
  ghci> :load BrokenClass
  [1 of 2] Compiling SimpleJSON       ( ../ch05/SimpleJSON.hs, interpreted )
  [2 of 2] Compiling BrokenClass      ( BrokenClass.hs, interpreted )
  Ok, modules loaded: SimpleJSON, BrokenClass.
\end{lstlisting}

然而当我们尝试\textit{使用}元组的列表时，错误便发生了。

\begin{lstlisting}[language=Haskell]
  ghci> toJValue [("foo","bar")]

  <interactive>:1:0:
      Overlapping instances for JSON [([Char], [Char])]
        arising from a use of `toJValue' at <interactive>:1:0-23
      Matching instances:
        instance (JSON a) => JSON [a]
          -- Defined at BrokenClass.hs:(44,0)-(46,25)
        instance (JSON a) => JSON [(String, a)]
          -- Defined at BrokenClass.hs:(50,0)-(52,25)
      In the expression: toJValue [("foo", "bar")]
      In the definition of `it': it = toJValue [("foo", "bar")]
\end{lstlisting}

重叠实例的问题是 Haskell 的开放世界假设的结果。下面用一个更简单的例子来说明到底发生了什么：

\begin{lstlisting}[language=Haskell]
  class Borked a where
    bork :: a -> String

  instance Borked Int where
    bork = show

  instance Borked (Int, Int) where
    bork (a, b) = bork a ++ ", " ++ bork b

  instance (Borked a, Borked b) => Borked (a, b) where
    bork (a, b) = ">>" ++ bork a ++ " " ++ bork b ++ "<<"
\end{lstlisting}

我们有两个 typeclass \acode{Borked} 的 pairs 实例：一对是\acode{Int}，另一对是其它任意值。

假设我们希望\acode{bork}一对\acode{Int}值，那么编译器必须选择一个实例来使用。由于这些实例相邻，那么看起来就能够简单的选择更加
明确的实例。

然而 GHC 默认是保守的，它会坚持必须只有一个可能的实例。因此最终在使用\acode{bork}时会抛出异常。

\begin{anote}
  \textbf{什么时候重叠的实例会有影响？}

  正如我们之前提到的那样，我们可以将一个 typeclass 的实例散落在若干模块中。GHC 并不会抱怨重叠实例的存在。而真正抱怨的时候就是
  当我们尝试使用这个受影响 typeclass 的方法时，也就是当强制要求选择哪一个实例需要使用的时候。
\end{anote}

\subsubsection*{放宽 typeclasses 的某些限制}

通常而言，我们不可以编写一个特定多态类型的 typeclass 实例。例如\acode{[Char]}类型就是\acode{[a]}指定\acode{Char}类型的
多态。因此禁止将\acode{[Char]}声明为 typeclass 的实例。这个非常的不方便，因为字符串在真实代码中处处存在。

\acode{TypeSynonymInstances}语言扩展移除了这个限制，允许我们编写上述的实例。

GHC 还支持另一个有用的语言扩展，\acode{OverlappingInstances}，专门用于处理重叠实例。当存在若干重叠实例需要选择时，该扩展使
编译器选择最明确的那个。

我们通常将该扩展与\acode{TypeSynoymInstances}一起使用。例如：

\begin{lstlisting}[language=Haskell]
  import Data.List

  class Foo a where
    foo :: a -> String

  instance {-# OVERLAPS #-} Foo a => Foo [a] where
    -- foo = concat . intersperse ", " . map foo
    foo = intercalate ", " . map foo

  instance {-# OVERLAPS #-} Foo Char where
    foo c = [c]

  instance {-# OVERLAPS #-} Foo String where
    foo = id

  main :: IO ()
  main = do
    putStrLn $ "foo: " ++ foo "SimpleClass"
    putStrLn $ "foo: " ++ foo ["a", "b", "c"]
\end{lstlisting}

与原文不同之处在于，注解\acode{\{-\# LANGUAGE OverlappingInstances \#-\}}在 6.8.1 后被弃用，现在则是在\acode{instance}
后使用\acode{\{-\# OVERLAPS \#-\}}来表示重叠的实例，
详见\href{https://ghc.gitlab.haskell.org/ghc/doc/users_guide/exts/instances.html#overlapping-instances}{文档}。

如果我们将\acode{foo}应用至一个\acode{String}，编译器将使用\acode{String}指定的实现。尽管存在\acode{[a]}与\acode{Char}
的\acode{Foo}实例，但是\acode{String}的实例更加的明确，因此 GHC 会选择它。

\subsection*{如何给类型一个新的身份}

略。

\begin{lstlisting}[language=Haskell]
  data DataInt = D Int
    deriving (Eq, Ord, Show)

  newtype NewtypeInt = N Int
    deriving (Eq, Ord, Show)
\end{lstlisting}

略。

总结：三总命名类型的方法：

\begin{itemize}
  \item \acode{data}关键字引入了一个真实的新的代数数据类型。
  \item \acode{type}关键字给予了已存在的类型一个同义词。我们可以替换的使用类型与其同义词。
  \item \acode{newtype}关键字给予已存在类型一个新的身份。原始类型和新的类型是不可替换的。
\end{itemize}

\subsection*{JSON typeclasses 没有重叠的实例}

现在需要帮助编译器区分 JSON 数组的\acode{[a]}，以及 JSON 对象的\acode{[(String,[a])]}。它们是造成重叠问题的原因。我们将列表
类型包裹起来，这样编译器则不会视其为列表：

\begin{lstlisting}[language=Haskell]
  newtype JAry a = JAry {fromJAry :: [a]}
    deriving (Eq, Ord, Show)
\end{lstlisting}

当需要将其从模块中导出时，我们需要导出该类型的所有细节。我们模块头看起来像是这样：

\begin{lstlisting}[language=Haskell]
  module JSONClass (JAry (..)) where
\end{lstlisting}

接下来是另一个包装类型用于隐藏 JSON 对象：

\begin{lstlisting}[language=Haskell]
  newtype JObj a = JObj {fromJObj :: [(String, a)]}
    deriving (Eq, Ord, Show)
\end{lstlisting}

有了这些类型定义，那么就可以对\acode{JValue}类型做点小修改：

\begin{lstlisting}[language=Haskell]
  data JValue
    = JString String
    | JNumber Double
    | JBool Bool
    | JNull
    | JObject (JObj JValue) -- was [(String, JValue)]
    | JArray (JAry JValue) -- was [JValue]
    deriving (Eq, Ord, Show)
\end{lstlisting}

这个改动并不会影响已经写过的\acode{JSON} typeclass 实例，不过还是需要为\acode{JAry}与\acode{JObj}类型编写\acode{JSON}
typeclass 实例。

\begin{lstlisting}[language=Haskell]
  jaryFromJValue :: (JSON a) => JValue -> Either JSONError (JAry a)

  jaryToJValue :: (JSON a) => JAry a -> JValue

  instance (JSON a) => JSON (JAry a) where
    toJValue = jaryToJValue
    fromJValue = jaryFromJValue
\end{lstlisting}

让我们慢慢的看一下将\acode{JAry a}转换成\acode{JValue}的每个步骤。给定一个列表，我们知道其所有元素都是\acode{JSON}实例，
将其转换成一个列表的\acode{JValue}很简单。

\begin{lstlisting}[language=Haskell]
  listToJValues :: (JSON a) => [a] -> [JValue]
  listToJValues = map toJValue
\end{lstlisting}

有了上述代码后，将其变为一个\acode{JAry JValue}就仅仅是应用\acode{newtype}类型构造函数即可：

\begin{lstlisting}[language=Haskell]
  jvaluesToJAry :: [JValue] -> JAry JValue
  jvaluesToJAry = JAry
\end{lstlisting}

（记住这并没有性能损耗，仅仅只是告诉编译器隐藏我们在使用一个列表的事实。）将其转换成一个\acode{JValue}，我们应用赢一个类型构造
函数：

\begin{lstlisting}[language=Haskell]
  jaryOfJValuesToJValue :: JAry JValue -> JValue
  jaryOfJValuesToJValue = JArray
\end{lstlisting}

将这些部分用函数组合的方式集成起来，就获得了一个简洁的一行代码转换成一个\acode{JValue}：

\begin{lstlisting}[language=Haskell]
  jaryToJValue = JArray . JAry . map toJValue . fromJAry
\end{lstlisting}

从\acode{JValue}转换至一个\acode{JAry a}则需要更多的工作，我们还是将其分解成可复用的部分。基础函数很直接：

\begin{lstlisting}[language=Haskell]
  jaryFromJValue (JArray (JAry a)) = whenRight JAry (mapEithers fromJValue a)
  jaryFromJValue _ = Left "not a JSON array"
\end{lstlisting}

\acode{whenRight}函数检查它的参数：如果是由\acode{Right}构造函数构建的它则调用一个函数，如果是由\acode{Left}构造则保留值
不变：

\begin{lstlisting}[language=Haskell]
  whenRight :: (b -> c) -> Either a b -> Either a c
  whenRight _ (Left err) = Left err
  whenRight f (Right a) = Right (f a)
\end{lstlisting}

更复杂的是\acode{mapEithers}，它的行为类似于普通的\acode{map}函数，但是如果遇到一个\acode{Left}值，它会立刻返回，而不是
继续累积一个\acode{Right}值的列表。

\begin{lstlisting}[language=Haskell]
  mapEithers :: (a -> Either b c) -> [a] -> Either b [c]
  mapEithers f (x : xs) = case mapEithers f xs of
    Left err -> Left err
    Right ys -> case f x of
      Left err -> Left err
      Right y -> Right (y : ys)
  mapEithers _ _ = Right []
\end{lstlisting}

由于隐藏在\acode{JObj}类型的列表中的元素有少许结构，因此它与\acode{JValue}之间的转换会变得更复杂。幸运的是我们可以复用刚刚
定义好的函数：

\begin{lstlisting}[language=Haskell]
  instance (JSON a) => JSON (JObj a) where
    toJValue = JObject . JObj . map (second toJValue) . fromJObj
    fromJValue (JObject (JObj o)) = whenRight JObj (mapEithers unwrap o)
      where
        unwrap (k, v) = whenRight (k,) (fromJValue v)
    fromJValue _ = Left "not a JSON object"
\end{lstlisting}

\end{document}
