\documentclass[./main.tex]{subfiles}

\begin{document}

\subsection*{在 Haskell 中表示 JSON}

首先是在 Haskell 中定义 JSON 这个数据，这里使用代数数据类型来表达 JSON 类型的范围。

\begin{lstlisting}[language=Haskell]
  data JValue
    = JString String
    | JNumber Double
    | JBool Bool
    | JNull
    | JObject [(String, JValue)]
    | JArray [JValue]
    deriving (Eq, Ord, Show)
\end{lstlisting}

对于每种 JSON 类型，我们都提供了独立的值构造函数。测试：

\begin{lstlisting}[language=Haskell]
  ghci> :l SimpleJSON
  [1 of 1] Compiling Main             ( SimpleJSON.hs, interpreted )
  Ok, one module loaded.
  ghci> JString "foo"
  JString "foo"
  ghci> JNumber 2.7
  JNumber 2.7
  ghci> :type JBool True
  JBool True :: JValue
\end{lstlisting}

构造一个从\acode{JValue}获取字符串的函数：

\begin{lstlisting}[language=Haskell]
  getString :: JValue -> Maybe String
  getString (JString s) = Just s
  getString _ = Nothing
\end{lstlisting}

测试：

\begin{lstlisting}[language=Haskell]
  ghci> :reload
  [1 of 1] Compiling Main             ( SimpleJSON.hs, interpreted )
  Ok, one module loaded.
  ghci> getString (JString "hello")
  Just "hello"
  ghci> getString (JNumber 3)
  Nothing
\end{lstlisting}

接下来是其它类型的访问函数：

\begin{lstlisting}[language=Haskell]
  getInt (JNumber n) = Just n
  getInt _ = Nothing

  getDouble (JNumber n) = Just n
  getDouble _ = Nothing

  getBool (JBool n) = Just n
  getBool _ = Nothing

  getObject (JObject o) = Just o
  getObject _ = Nothing

  getArray (JArray a) = Just a
  getArray _ = Nothing

  isNull v = v == JNull
\end{lstlisting}

\acode{truncate}函数可以让浮点类型或者有理数去掉小数点后变为整数：

\begin{lstlisting}[language=Haskell]
  ghci> truncate 5.8
  5
  ghci> :module +Data.Ratio
  ghci> truncate (22 % 7)
  3
\end{lstlisting}

\subsection*{Haskell 模块详解}

一个 Haskell 源文件包含了单个\textit{模块}的定义。模块允许我们在它其内部进行定义，并由其它模块访问：

\begin{lstlisting}[language=Haskell]
  module SimpleJSON
    ( JValue (..),
      getString,
      getInt,
      getDouble,
      getBool,
      getObject,
      getArray,
      isNull,
    )
  where
\end{lstlisting}

如果省略了导出（即圆括号以及其所包含的名称），那么该模块中的所有名称都会被导出。

\subsection*{编译 Haskell 源}

编译一个源文件：

\begin{lstlisting}[language=Bash]
  ghc -c SimpleJSON.hs
\end{lstlisting}

\acode{-c}选项告诉\textbf{ghc}仅生成对象代码。如果省略了该选项，那么编译器则会尝试生成一整个可执行文件。这会导致失败，因为
我们并没有一个\acode{main}函数，即 GHC 所认为的一个独立程序的执行入口。

编译后会得到两个新文件：\acode{SimpleJSON.hi}与\acode{SimpleJSON.o}。前者是一个\textit{接口 interface}文件，即
\textbf{ghc}以机器码的形式存储模块导出的名称信息；后者是一个\textit{对象 object}文件，其包含了生产的机器码。

\subsection*{生成一个 Haskell 程序，导入模块}

添加一个\acode{Main.hs}文件，其内容如下：

\begin{lstlisting}[language=Haskell]
  module Main where

  import SimpleJSON

  main = print (JObject [("foo", JNumber 1), ("bar", JBool False)])
\end{lstlisting}

与原文\acode{module Main () where}的不同之处在于，现在的\acode{Main}后不再需要一个\acode{()}。

接下来是编译\acode{main}函数：

\begin{lstlisting}[language=Bash]
  ghc -o simple Main.hs
\end{lstlisting}

与原文\acode{ghc -o simple Main.hs SimpleJSON.o}不同，现在没了\acode{SimpleJSON.o}这个文件，加上后会报错重复
symbol 的编译错误（因为在\acode{Main.hs}中已经做了\acode{import SimpleJSON}导入了）。

这次省略掉了\acode{-c}选项，因此编译器尝试生成一个可执行。生成可执行的这个过程被称为\textit{链接 linking}（与 C++ 一样），
即在一次编译中链接源文件与可执行文件。

这里给了\textbf{ghc}一个新选项\acode{-o}，其接受一个参数：可执行文件的名称，这里是\acode{simple}，执行它：

\begin{lstlisting}[language=Bash]
  ./simple
  JObject [("foo",JNumber 1.0),("bar",JBool False)]
\end{lstlisting}

\subsection*{打印 JSON 数据}

现在我们希望将 Haskell 的值渲染成 JSON 数据，创建一个\acode{PutJSON.hs}文件：

\begin{lstlisting}[language=Haskell]
  module PutJSON where

  import Data.List (intercalate)
  import SimpleJSON

  renderJValue :: JValue -> String
  renderJValue (JString s) = show s
  renderJValue (JNumber n) = show n
  renderJValue (JBool True) = "true"
  renderJValue (JBool False) = "false"
  renderJValue JNull = "null"
  renderJValue (JObject o) = "{" ++ pairs o ++ "}"
    where
      pairs [] = ""
      pairs ps = intercalate ", " (map renderPair ps)
      renderPair (k, v) = show k ++ ": " ++ renderJValue v
  renderJValue (JArray a) = "[" ++ values a ++ "]"
    where
      values [] = ""
      values vs = intercalate ", " (map renderJValue vs)
\end{lstlisting}

好的 Haskell 风格需要分隔纯代码与 I/O 代码。我们的\acode{renderJValue}函数不会与外界交互，但是仍然需要一个打印的函数：

\begin{lstlisting}[language=Haskell]
  putJValue :: JValue -> IO ()
  putJValue = putStrLn . renderJValue
\end{lstlisting}

\subsection*{类型推导是把双刃剑}

假设我们编写了一个自认为返回\acode{String}的函数，但是并不为其写类型签名：

\begin{lstlisting}[language=Haskell]
  upcaseFirst (c:cs) = toUpper c -- forgot ":cs" here
\end{lstlisting}

这里希望的是单词首字母大写，但是忘记了将剩余的字符放进结果中。我们认为函数的类型是\acode{String -> String}，但是编译器则会
将其视为\acode{String -> Char}。假设我们尝试在其他地方调用该函数：

\begin{lstlisting}[language=Haskell]
  camelCase :: String -> String
  camelCase xs = concat (map upcaseFirst (words xs))
\end{lstlisting}

那么当我们尝试编译该代码或者是加载进\textbf{ghci}，我们并不会得到明显的错误信息：

\begin{lstlisting}[language=Haskell]
  ghci> :load Trouble
  [1 of 1] Compiling Main             ( Trouble.hs, interpreted )

  Trouble.hs:9:27:
      Couldn't match expected type `[Char]' against inferred type `Char'
        Expected type: [Char] -> [Char]
        Inferred type: [Char] -> Char
      In the first argument of `map', namely `upcaseFirst'
      In the first argument of `concat', namely
          `(map upcaseFirst (words xs))'
  Failed, modules loaded: none.
\end{lstlisting}

注意这里的报错是在使用\acode{upcaseFirst}函数处，那么假设我们认为\acode{upcaseFirst}的定义与类型是正确的，那么查找到真正的
错误可能会花掉一些时间。

\subsection*{更加泛用的渲染}

我们将更为泛用的打印模块称为\acode{Prettify}，那么其源文件即\acode{Prettify.hs}。

为了使\acode{Prettify}满足实际需求，我们还要一个新的 JSON 渲染器来使用\acode{Prettify}的API。在\acode{Prettify}模块中将
使用一个抽象类型\acode{Doc}。基于建立在抽象类型的泛用渲染库，我们可以选择灵活高效的实现。

\acode{PrettyJSON.hs}示例：

\begin{lstlisting}[language=Haskell]
  renderJValue :: JValue -> Doc
  renderJValue (JBool True)  = text "true"
  renderJValue (JBool False) = text "false"
  renderJValue JNull         = text "null"
  renderJValue (JNumber num) = double num
  renderJValue (JString str) = string str
\end{lstlisting}

这里的\acode{text}，\acode{double}以及\acode{string}都会在\acode{Prettify}模块中提供。

\subsection*{开发 Haskell 代码而不发疯}

一个用于快速开发程序框架的技巧就是编写占位符，或者类型与函数的\textit{根 stub}版本。例如上述\acode{string}，\acode{text}
以及\acode{double}函数将被\acode{Prettify}模块提供。如果我们没有提供这些函数或者\acode{Doc}类型，那么“早点编译，经常编译”
这个尝试就会失败。为了避免这个问题现在让我们编写一个不做任何事情的根程序。

\begin{lstlisting}[language=Haskell]
  import SimpleJSON

  data Doc = ToBeDefined deriving (Show)

  string :: String -> Doc
  string str = undefined

  text :: String -> Doc
  text str = undefined

  double :: Double -> Doc
  double num = undefined
\end{lstlisting}

特殊值\acode{undefined}有一个\acode{a}类型，无论在哪里使用它，总是会有类型检查。如果尝试计算，则会使程序崩溃：

\begin{lstlisting}[language=Haskell]
  ghci> :type undefined
  undefined :: a
  ghci> undefined
  *** Exception: Prelude.undefined
  ghci> :type double
  double :: Double -> Doc
  ghci> double 3.14
  *** Exception: Prelude.undefined
\end{lstlisting}

尽管还不能运行根代码，但是编译器的类型检查器可以确保我们的程序类型正确。

\subsection*{漂亮的打印字符串}

当需要打印一个漂亮的字符串时，我们必须遵循 JSON 的转义规则。字符串就是一系列被包裹在引号中的字符们。\acode{PrettyJSON.hs}：

\begin{lstlisting}[language=Haskell]
  string :: String -> Doc
  string = enclose '"' '"' . hcat . map oneChar
\end{lstlisting}

以及\acode{enclose}函数将一个\acode{Doc}值简单的包裹在一个开始与结束字符之间：

\begin{lstlisting}[language=Haskell]
  enclose :: Char -> Char -> Doc -> Doc
  enclose left right x = char left <> x <> char right
\end{lstlisting}

这里提供的\acode{<>}函数定义在\acode{Prettify}库中，它需要两个\acode{Doc}值，即\acode{Doc}版本的\acode{(++)}。还是
先在根文件中定义：

\begin{lstlisting}[language=Haskell]
  (<>) :: Doc -> Doc -> Doc
  a <> b = undefined

  char :: Char -> Doc
  char c = undefined
\end{lstlisting}

我们的库还需要提供\acode{hcat}，将若干\acode{Doc}值合成成为一个（类似于列表的\acode{concat}）：

\begin{lstlisting}[language=Haskell]
  hcat :: [Doc] -> Doc
  hcat xs = undefined
\end{lstlisting}

我们的\acode{string}函数应用\acode{oneChar}函数到字符串中的每个字符，将它们连接，然后用引号包装。而\acode{oneChar}函数
则是用来转义或包装一个独立的字符。在\acode{PrettyJSON.hs}中：

\begin{lstlisting}[language=Haskell]
  oneChar :: Char -> Doc
  oneChar c = case lookup c simpleEscapes of
    Just r -> text r
    Nothing
      | mustEscape c -> hexEscape c
      | otherwise -> char c
    where
      mustEscape c = c < ' ' || c == '\x7f' || c > '\xff'

  simpleEscapes :: [(Char, String)]
  simpleEscapes = zipWith ch "\b\n\f\r\t\\\"/" "bnfrt\\\"/"
    where
      ch a b = (a, ['\\', b])
\end{lstlisting}

这里的\acode{simpleEscapes}是一个列表的二元组，我们称其为\textit{关联 association}列表，或者简称\acode{alist}。每个
\acode{alist}的元素都将字符关联了它的转义表达，测试：

\begin{lstlisting}[language=Haskell]
  ghci> take 4 simpleEscapes
  [('\b',"\\b"),('\n',"\\n"),('\f',"\\f"),('\r',"\\r")]
\end{lstlisting}

我们的\acode{case}表达式尝试查看字符是否匹配关联列表。如果匹配则返回匹配值，如若不然则需要以更复杂的方式来转义该字符。只有当
两种转义都不需要时，才会返回普通字符。保守起见，我们输出的唯一未转义字符是可打印的 ASCII 字符。

更复杂的转义包含了将一个字符转为字符串\acode{"\\u"}并跟随四个十六进制的字符用于表达 Unicode 字符的数值。仍然是
\acode{PrettyJSON.hs}：

\begin{lstlisting}[language=Haskell]
  smallHex :: Int -> Doc
  smallHex x =
    text "\\u"
      <> text (replicate (4 - length h) '0')
      <> text h
    where
      h = showHex x ""
\end{lstlisting}

这里的\acode{showHex}函数需要从\acode{Numeric}库中加载，其用于返回一个值的十六进制：

\begin{lstlisting}[language=Haskell]
  ghci> showHex 114111 ""
  "1bdbf"
\end{lstlisting}

\acode{replicate}函数则是由 Prelude 提供：

\begin{lstlisting}[language=Haskell]
  ghci> replicate 5 "foo"
  ["foo","foo","foo","foo","foo"]
\end{lstlisting}

\acode{smallHex}提供的四位编码只能表示最大\acode{0xffff}的 Unicode 字符，而有效的 Unicode 字符的范围可以达到
\acode{0x10ffff}。为了正确的表达一个超出了\acode{0xffff}的 JSON 字符串，我们遵循一些复杂的规则将其分为两部分。这使我们
有机会对 Haskell 的数执行一些位级操作。还是\acode{PrettyJSON.hs}：

\begin{lstlisting}[language=Haskell]
  astral :: Int -> Doc
  astral n = smallHex (a + 0xd800) <> smallHex (b + 0xdc00)
    where
      a = (n `shiftR` 10) .&. 0x3ff
      b = n .&. 0x3ff
\end{lstlisting}

这里\acode{shiftR}函数和\acode{(.&.)}函数都源自\acode{Data.Bits}模块，前者将一个数移动右一位，后者则是执行一个字节层面的
两值\textit{and}操作。

\begin{lstlisting}[language=Haskell]
  ghci> 0x10000 `shiftR` 4   :: Int
  4096
  ghci> 7 .&. 2   :: Int
  2
\end{lstlisting}

现在有了\acode{smallHex}与\acode{astral}，我们可以提供\acode{hexEscape}的定义了：

\begin{lstlisting}[language=Haskell]
  hexEscape :: Char -> Doc
  hexEscape c
    | d < 0x10000 = smallHex d
    | otherwise = astral (d - 0x10000)
    where
      d = ord c
\end{lstlisting}

其中\acode{ord}由\acode{Data.Char}模块提供。

\subsection*{数组与对象，以及模块头}

相比于字符串的漂亮打印，数组和对象就是小菜一碟了。我们已经知道了它们两者其实很相似：都是由起始字符开始，接着是一系列由逗号分隔
的值，最后接上结束字符。让我们编写一个函数捕获数组与对象的共同结构：

\begin{lstlisting}[language=Haskell]
  series :: Char -> Char -> (a -> Doc) -> [a] -> Doc
  series open close item = enclose open close . fsep . punctuate (char ',') . map item
\end{lstlisting}

让我们首先从函数类型开始。它接受起始与结束字符，一个打印某些未知类型\acode{a}值的函数，以及一个类型为\acode{a}的列表，返回
一个类型为\acode{Doc}的值。

注意尽管我们的类型签名提及了四个参数，在函数定义中仅列出了三个。这遵循了简化定义的规则，如\acode{myLength xs = length xs}
等同于\acode{myLength = length}。

我们已经有了之前编写过的\acode{enclose}，即包装一个\acode{Doc}值进起始与结束字符之间。那么\acode{fsep}函数则位于
\acode{Prettify}模块中，其结合一个\acode{Doc}值列表成为一个\acode{Doc}，在输出不适合单行的情况下还需要换行。

\begin{lstlisting}[language=Haskell]
  fsep :: [Doc] -> Doc
  fsep xs = undefined
\end{lstlisting}

那么现在，遵循上述提供的例子，你应该能够定义你自己的\acode{Prettify.hs}的根文件了。这里不再显式的定义更多的根了。

\acode{puctuate}函数同样位于\acode{Prettify}模块中：

\begin{lstlisting}[language=Haskell]
  punctuate :: Doc -> [Doc] -> [Doc]
  punctuate p [] = []
  punctuate p [d] = [d]
  punctuate p (d : ds) = (d <> p) : punctuate p ds
\end{lstlisting}

通过\acode{series}的定义，漂亮打印一个数组就非常直接了：

\begin{lstlisting}[language=Haskell]
  renderJValue (JArray ary) = series '[' ']' renderJValue ary
\end{lstlisting}

对于对象而言，还需要额外的一些工作：每个元素同时需要处理名称与值：

\begin{lstlisting}[language=Haskell]
  renderJValue (JObject obj) = series '{' '}' field obj
  where
    field (name, val) =
      string name
        PrettyStub.<> text ": "
        PrettyStub.<> renderJValue val
\end{lstlisting}

\subsection*{编写一个模块头}

现在已经有了\acode{PrettyJSON.hs}文件，我们需要回到其顶部添加模块声明：

\begin{lstlisting}[language=Haskell]
  module PrettyJSON (renderJValue) where
\end{lstlisting}

这里导出了一个名称：\acode{renderJValue}，也就是我们的 JSON 渲染函数。模块中其它的定义都是用于支持\acode{renderJValue}的，
因此没有必要对其它模块可见。

\subsection*{充实我们的漂亮打印库}

在\acode{Prettify}模块中，提供了\acode{Doc}类型作为一个代数数据类型：

\begin{lstlisting}[language=Haskell]
  data Doc
    = Empty
    | Char Char
    | Text String
    | Line
    | Concat Doc Doc
    | Union Doc Doc
    deriving (Show)
\end{lstlisting}

观察可知\acode{Doc}类型实际上是一颗树。\acode{Concat}与\acode{Union}构造函数根据其它两个\acode{Doc}值创建一个内部节点，而
\acode{Empty}以及其它简单的构造函数用于构建叶子。

在模块的头部，我们导出该类型的名称，而不是它们的构造函数：这样可以防止使用 Doc 的构造函数被用于创建与模式匹配\acode{Doc}值。

相反的，要创建一个\acode{Doc}，用户需要调用我们在\acode{Prettify}模块中所提供的函数：

\begin{lstlisting}[language=Haskell]
  empty :: Doc
  empty = Empty

  char :: Char -> Doc
  char = Char

  text :: String -> Doc
  text "" = Empty
  text s = Text s

  double :: Double -> Doc
  double = text . show
\end{lstlisting}

\acode{Line}构造函数代表一个换行，其创建的是一个\textit{hard}换行，即总是会在漂亮的打印中出现。有时我们想要一个\textit{soft}
换行，即只会在窗口或者页面上过长显示时才会换行。稍后将会介绍\acode{softline}函数。

\begin{lstlisting}[language=Haskell]
  line :: Doc
  line = Line
\end{lstlisting}

另外就是用于连接两个\acode{Doc}值的\acode{(<>)}函数（这里使用\acode{(<+>)}，因为\acode{(<>)}在 Prelude 中已经有定义了）：

\begin{lstlisting}[language=Haskell]
  (<+>) :: Doc -> Doc -> Doc
  Empty <+> y = y
  x <+> Empty = x
  x <+> y = x `Concat` y
\end{lstlisting}

测试：

\begin{lstlisting}[language=Haskell]
  ghci> text "foo" <> text "bar"
  Concat (Text "foo") (Text "bar")
  ghci> text "foo" <> empty
  Text "foo"
  ghci> empty <> text "bar"
  Text "bar"
\end{lstlisting}

接下来是用于连接\acode{Doc}列表的\acode{hcat}函数：

\begin{lstlisting}[language=Haskell]
  hcat :: [Doc] -> Doc
  hcat = fold (<+>)

  fold :: (Doc -> Doc -> Doc) -> [Doc] -> Doc
  fold f = foldr f empty
\end{lstlisting}

以及\acode{fsep}函数，它还依赖若干其它函数：

\begin{lstlisting}[language=Haskell]
  fsep :: [Doc] -> Doc
  fsep = fold (</>)

  (</>) :: Doc -> Doc -> Doc
  x </> y = x <+> softline <+> y

  softline :: Doc
  softline = group line
\end{lstlisting}

这里需要解释一下，\acode{softline}函数应该在当前行特别宽的时候进行换行，否则插入空格。如果我们的\acode{Doc}类型不包含任何关于
渲染的信息，那么该如何呢？答案就是每次遇到一个软换行，通过\acode{Union}构造函数来维护两个可选项：

\begin{lstlisting}[language=Haskell]
  group :: Doc -> Doc
  group x = flatten x `Union` x
\end{lstlisting}

\acode{flatten}函数将一个\acode{Line}替换为空格，将两行转换为一个更长的行。

\begin{lstlisting}[language=Haskell]
  flatten :: Doc -> Doc
  flatten (x `Concat` y) = flatten x `Concat` flatten y
  flatten Line = Char ' '
  flatten (x `Union` _) = flatten x
  flatten other = other
\end{lstlisting}

注意总是调用\acode{flatten}在\acode{Union}的左元素上：每个\acode{Union}的左侧总是大于等于右侧宽度（字符距离）。

\subsubsection*{紧密渲染}

我们需要频繁的使用包含尽可能少字符的数据。例如通过网络连接发送 JSON 数据就没有必要美观：软件并不在乎数据的美观与否，添加很多的空格
只会带来性能下降。

因此我们提供了一个紧密渲染的函数：

\begin{lstlisting}[language=Haskell]
  compact :: Doc -> String
  compact x = transform [x]
    where
      transform [] = ""
      transform (d : ds) = case d of
        Empty -> transform ds
        Char c -> c : transform ds
        Text s -> s ++ transform ds
        Line -> '\n' : transform ds
        a `Concat` b -> transform (a : b : ds)
        _ `Union` b -> transform (b : ds)
\end{lstlisting}

\acode{compact}函数将其参数包裹成一个列表，然后将帮助函数\acode{transform}应用至该列表。\acode{transform}函数视其参数为
堆叠的项用于处理，列表的第一个元素即堆的顶部。

\acode{transform}函数的\acode{(d:ds)}模式将堆顶部的元素取出\acode{d}并留下\acode{ds}。在\acode{case}表达式中，前面几个
分支都在\acode{ds}上递归，每次递归消费堆顶部的元素；最后两个分支则是在\acode{ds}之前添加项：\acode{Concat}添加两个元素至堆，
而\acode{Union}分支则忽略它左侧元素，调用的是\acode{flatten}，再将其右侧元素至堆。

测试\acode{compact}：

\begin{lstlisting}[language=Haskell]
  ghci> let value = renderJValue (JObject [("f", JNumber 1), ("q", JBool True)])
  ghci> :type value
  value :: Doc
  ghci> putStrLn (compact value)
  {"f": 1.0,
  "q": true
  }
\end{lstlisting}

为了更好的理解代码是如何运作的，让我们用一个更简单的例子来展示细节：

\begin{lstlisting}[language=Haskell]
  ghci> char 'f' <> text "oo"
  Concat (Char 'f') (Text "oo")
  ghci> compact (char 'f' <> text "oo")
  "foo"
\end{lstlisting}

当我们应用\acode{compact}时，它会将它的参数转为一个列表后应用函数\acode{transform}。

\begin{itemize}
  \item 接下来\acode{transform}函数接受了一个单例列表，然后进行模式匹配\acode{(d:ds)}，这里的\acode{d}是
        \acode{Concat (Char 'f') (Text "oo")}，而\acode{ds}则是一个空列表。

        由于\acode{d}的构造函数是\acode{Concat}，其模式匹配就在\acode{case}表达式中。那么在右侧，添加\acode{Char 'f'}与
        \acode{Text "oo"}值堆，然后递归的应用\acode{transform}。
  \item \begin{itemize}
          \item \acode{transform}函数接受了一个包含两项的列表，继续匹配\acode{d:ds}模式。此时变量\acode{d}绑定到了
                \acode{Char 'f'}，而\acode{ds}则是\acode{[Text "oo"]}。

                \acode{case}表达式匹配到了\acode{Char}分支。那么在右侧，使用\acode{(:)}来构建一个列表，其头部为\acode{'f'}，
                其余部分则是递归应用\acode{transform}后的结果。
          \item \begin{itemize}
                  \item 递归的调用接受到一个单例列表，其变量\acode{d}绑定至\acode{Text "oo"}，\acode{ds}绑定至\acode{[]}。

                        \acode{case}表达式匹配\acode{Text}分支。那么在右侧，使用\acode{(++)}来连接\acode{"oo"}与递归调用
                        \acode{transform}后的结果。
                  \item \begin{itemize}
                          \item 最后的调用，\acode{transform}得到一个空列表，返回一个空字符串。
                        \end{itemize}
                  \item 结果是\acode{"oo" ++ ""}
                \end{itemize}
          \item 结果是\acode{'f' : "oo" ++ ""}
        \end{itemize}
\end{itemize}

\subsubsection*{真实的漂亮打印}

我们的\acode{compact}函数对于机器之间的交流是有帮助的，但是它的结果对人类而言并不易读：每行的信息很少。为了生成一个更可读的输出，
我们将要编写另一个函数\acode{pretty}。相比于\acode{compact}，\acode{pretty}接受一个额外的参数：一行的最大宽度。

\begin{lstlisting}[language=Haskell]
  pretty :: Int -> Doc -> String
\end{lstlisting}

确切来说，\acode{Int}参数控制了\acode{pretty}在遇到一个\acode{softline}时的行为。在一个\acode{softline}时，\acode{pretty}
会选择继续留在当前行还是另起一行。其余情况下，必须严格遵守漂亮打印函数所设定的指令。

以下是实现的代码：

\begin{lstlisting}[language=Haskell]
  pretty width x = best 0 [x]
  where
    best col (d : ds) = case d of
      Empty -> best col ds
      Char c -> c : best (col + 1) ds
      Text s -> s ++ best (col + length s) ds
      Line -> '\n' : best 0 ds
      a `Concat` b -> best col (a : b : ds)
      a `Union` b -> nicest col (best col (a : ds)) (best col (b : ds))
    best _ _ = ""
    nicest col a b
      | (width - least) `fits` a = a
      | otherwise = b
      where
        least = min width col
\end{lstlisting}

\acode{best}帮助函数接受两个参数：当前行使用了的列数，以及剩余的仍需处理的\acode{Doc}列表。

在简单的情况下，随着消费输入\acode{best}直接更新了\acode{col}变量。其中\acode{Concat}情况也很明显：将两个连接过的部分推至堆叠，
且不触碰\acode{col}。

有趣的情况在\acode{Union}构造函数。回想一下之前将\acode{flatten}应用至左侧元素，且不对右侧做任何操作。还有就是\acode{flatten}
将新行替换成空格。因此我们则需要检查这两种布局，\acode{flatten}后的还是原始的，更适合我们的宽度限制。

为此需要编写一个小的帮助函数来决定\acode{Doc}值的一行是否适合给定的长度：

\begin{lstlisting}[language=Haskell]
  fits :: Int -> String -> Bool
  w `fits` _ | w < 0 = False
  w `fits` "" = True
  w `fits` ('\n' : _) = True
  w `fits` (c : cs) = (w - 1) `fits` cs
\end{lstlisting}

\subsubsection*{遵循漂亮打印}

为了理解代码如何工作的，首先考虑一个简单的\acode{Doc}值：

\begin{lstlisting}[language=Haskell]
  ghci> empty </> char 'a'
  Concat (Union (Char ' ') Line) (Char 'a')
\end{lstlisting}

我们将应用\acode{pretty 2}在该值。当我们首先应用\acode{best}，\acode{col}值为零。它匹配\acode{Concat}模式，接着将
\acode{Union (Char ' ') Line}与\acode{Char 'a'}推至堆，接着是递归应用自身，它匹配了\acode{Union (Char ' ') Line}。

此时我们忽略 Haskell 通常的计算顺序。现在有了两个子表达式，\acode{best 0 [Char ' ', char 'a']}以及
\acode{best 0 [Line, Char 'a']}。前者计算得到\acode{" a"}，而后者得到\acode{"\na"}。接着将它们替换到外层的表达式中，得到
\acode{nicest 0 " a" "\\na"}。

为了明白\acode{nicest}的结果，我们做一个小小的替换。\acode{width}以及\acode{col}分别是 0 与 2，那么\acode{least}就是 0，
\acode{width - least}就是 2。这里用\textbf{ghci}来计算一下\acode{2 `fits` " a"}：

\begin{lstlisting}[language=Haskell]
  ghci> 2 `fits` " a"
  True
\end{lstlisting}

计算得到\acode{True}，那么这里的\acode{nicest}结果就是\acode{" a"}。

如果将\acode{pretty}函数应用到之前同样的 JSON 数据上，可以看到根据提供的最大长度，它会得到不同的结果：

\begin{lstlisting}[language=Haskell]
  ghci> putStrLn (pretty 10 value)
  {"f": 1.0,
  "q": true
  }
  ghci> putStrLn (pretty 20 value)
  {"f": 1.0, "q": true
  }
  ghci> putStrLn (pretty 30 value)
  {"f": 1.0, "q": true }
\end{lstlisting}

\subsection*{创建一个库}

% TODO

% \begin{lstlisting}[language=Haskell]

% \end{lstlisting}


\end{document}
