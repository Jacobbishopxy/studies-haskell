\documentclass[./main.tex]{subfiles}

\begin{document}

\subsection*{高效文件处理}

下面是一个简单的微基准测试，读取一个全是数字的文本文件，打印它们的总和：

\begin{lstlisting}[language=Haskell]
  main :: IO ()
  main = do
    contents <- getContents
    print $ sumFile contents
    where
      sumFile = sum . map read . words
\end{lstlisting}

尽管\acode{String}是用于读写文件的默认类型，它并不高效，因此像这样一个简单的程序性能是很差的。

\acode{String}代表着\acode{Char}值的列表；列表中每个元素的内存分配都是独立的，需要额外的开销。这些因素会影响必须读写文本或
二进制数据程序的内存消耗和性能。像这样的简单基准测试，即使是用解释性语言（如 Python）编写的程序，性能也比使用\acode{String}
的 Haskell 代码高出一个数量级。

\acode{bytestring}库提供了一个快速并且又低开销的\acode{String}类型替代品。通过\acode{bytestring}编写的代码通常可以
媲美或超过 C 语言的性能和内存消耗，同时还保持了 Haskell 的表现力和简洁性。

该库提供了两个模块。每个定义的函数几乎都是\acode{String}对应函数的直接替代品：

\begin{itemize}
  \item \acode{Data.ByteString}模块定义了一个\textit{严格 strict}类型的\acode{ByteString}。其以二进制或文本数据的
        数组形式来表示字符串。
  \item \acode{Data.ByteString.Lazy}模块提供了\textit{惰性 lazy}类型的\acode{ByteString}。其以\textit{块 chunks}
        数组来表示字符串，最大大小为 64KB。
\end{itemize}

两个\acode{ByteString}类型在特定环境都有更好的表现。对于流式处理一个大数据（几百 MB 至 TB），惰性\acode{ByteString}类型
表现的最好。它的块大小被调整为适合现代 CPU 的 L1 缓存，且垃圾回收可以快速丢弃不再使用的流数据块。

而严格的\acode{ByteString}类型在不太关心内存占用或需要随机访问数据的环境下性能更好。

\subsubsection*{二进制 I/O 与 qualified imports}

现在来开发一个函数来说明\acode{ByteString}的一些 API。我们将决定一个文件是否为 ELF 对象：这种格式多用于现代 Unix 类型系统的
可执行文件。

查看文件的首四个字节，检查它们是否匹配一个字节列表：

\begin{lstlisting}[language=Haskell]
  import Data.ByteString.Lazy qualified as L

  hasElfMagic :: L.ByteString -> Bool
  hasElfMagic content = L.take 4 content == elfMagic
    where
      elfMagic = L.pack [0x7f, 0x45, 0x4c, 0x46]
\end{lstlisting}

\acode{ByteString}的惰性模块以及严格模块意图解决二进制 I/O。Haskell 用 \acode{Word8} 来展示字节的数据类型；可以通过
\acode{Data.Word}来引入它。

上述例子的\acode{L.pack}函数接受一个\acode{Word8}列表，打包它们成为一个惰性的\acode{ByteString}。（\acode{L.unpack}
函数作用相反。）而\acode{hasElfMagic}函数则是比较\acode{ByteString}的前四个字符。

以下是将其运用在一个文件上的例子：

\begin{lstlisting}[language=Haskell]
  isElfFile :: FilePath -> IO Bool
  isElfFile path = do
    content <- L.readFile path
    return $ hasElfMagic content
\end{lstlisting}

\acode{L.readFile}函数是\acode{readFile}的惰性\acode{ByteString}版本，即按需读取。它效率很高，每次读取最大 64KB。惰性
\acode{ByteString}很适合我们的任务：因为我们至多读取文件的前四个字节，因此我们可以安全的使用该函数在任何大小的文件上。

\subsubsection*{Text I/O}

\acode{bytestring}库还提供了两个模块用于 text I/O 功能，\acode{Data.ByteString.Char8}以及
\acode{Data.ByteString.Lazy.Char8}。它们将单独的字符串元素导出成\acode{Char}而不是\acode{Word8}。

\begin{awarn}
  上述模块中的函数进作用于字节大小的\acode{Char}值，也就是仅适用于 ASCII 以及某些欧洲字符集。超过 255 的字符会被截断。
\end{awarn}

面向字符的\acode{bytestring}模块为文本处理提供了一些有用的函数。以下是包含了月度的股票价格的文件：

\begin{lstlisting}[language=Haskell]
  ghci> putStr =<< readFile "prices.csv"
  Date,Open,High,Low,Close,Volume,Adj Close
  2008-08-01,20.09,20.12,19.53,19.80,19777000,19.80
  2008-06-30,21.12,21.20,20.60,20.66,17173500,20.66
  2008-05-30,27.07,27.10,26.63,26.76,17754100,26.76
  2008-04-30,27.17,27.78,26.76,27.41,30597400,27.41
\end{lstlisting}

如何找到最高价呢？收盘价在第四列，用都好分隔。下面是得到收盘价的函数：

\begin{lstlisting}[language=Haskell]
  closing = readPrice . (!! 4) . L.split ','
\end{lstlisting}

该函数是 point-free 风格，需要从右往左阅读。\acode{L.split}将一个惰性的\acode{ByteString}分隔开来，分隔发生在每一次找到
匹配值时。\acode{(!!)}操作符则是获取列表中第 k 个元素。\acode{readPrice}函数将一个代表分数的字符串转换为数值：

\begin{lstlisting}[language=Haskell]
  readPrice :: L.ByteString -> Maybe Int
  readPrice str = case L.readInt str of
    Nothing -> Nothing
    Just (dollars, rest) ->
      case L.readInt (L.tail rest) of
        Nothing -> Nothing
        Just (cents, more) ->
          Just (dollars * 100 + cents)
\end{lstlisting}

上面使用了\acode{L.readInt}函数，即解析整数，返回整数以及字符串剩余部分。

找到最高收盘价的函数：

\begin{lstlisting}[language=Haskell]
  highestClose = maximum . (Nothing :) . map closing . L.lines

  highestCloseFrom path = do
    contents <- L.readFile path
    print $ highestClose contents
\end{lstlisting}

这里使用了一个小技巧。当我们提供一个空列表给\acode{maximum}函数会抛出异常：

\begin{lstlisting}[language=Haskell]
  ghci> maximum [3,6,2,9]
  9
  ghci> maximum []
  *** Exception: Prelude.maximum: empty list
\end{lstlisting}

测试：

\begin{lstlisting}[language=Haskell]
  ghci> :load HighestClose
  [1 of 1] Compiling Main             ( HighestClose.hs, interpreted )
  Ok, modules loaded: Main.
  ghci> highestCloseFrom "prices.csv"
  Loading package array-0.1.0.0 ... linking ... done.
  Loading package bytestring-0.9.0.1 ... linking ... done.
  Just 2741
\end{lstlisting}

因为从逻辑中分离了 I/O，测试空数据时不需要创建一个空文件：

\begin{lstlisting}[language=Haskell]
  ghci> highestClose L.empty
  Nothing
\end{lstlisting}

\subsection*{文件名匹配}

很多面向系统的编程语言都提供了根据模式来匹配文件的功能。尽管 Haskell 的标准库有不错的系统编程工具，它并没有提供这类的匹配函数。

这类模式通常被称为\acode{glob 模式}，通配符模式或者 shell 样式模式。它们有一些简单的规则：

\begin{itemize}
  \item 将字符串与模式进行匹配，从字符串的开头开始，到末尾结束。
  \item 大多字面字符都匹配自己。例如，模式中的文本 foo 将匹配输入字符串中的 foo，且仅匹配 foo。
  \item \acode{*}（星号）字符表示“匹配任何内容“；它将匹配任何文本，包括空字符串。
  \item \acode{?}（问号）字符匹配任何单个字符。模式\acode{pic??.jpg}将匹配\acode{picaa.jpg}或\acode{pic01.jpg}等名称。
  \item \acode{[}（开方括号）字符开始一个字符类，以\acode{]}结束。它的意思是“匹配这个类中的任何字符”。字符类可以在\acode{[}
        后面加一个\acode{!}来表示“匹配不属于这个类的任何字符”。

        作为一种简写，一个字符后面跟着一个\acode{-}（破折号），后面跟着另一个字符，表示一个范围：”匹配此集合中的任何字符“
\end{itemize}

虽然 Haskell 的标准库中没有提供 glob 模式的匹配，但是它提供了一个非常好的正则表达式库。

\subsection*{Haskell 中的正则表达式}

首先让我们加载\acode{Text.Regex.Posix}库

\begin{lstlisting}[language=Haskell]
  ghci> :module +Text.Regex.Posix
\end{lstlisting}

根据\href{https://wiki.haskell.org/Regular_expressions}{官方 Wiki}提到的\acode{regex-posix}速度非常的慢，在生产代码
中需要换成别的库。

我们这里只需要正则表达式匹配的函数，一个中缀操作符\acode{(=~)}即可（从 Perl 中借用）。要克服的第一个障碍是 Haskell 的 regexp
库大量使用了多态。因此\acode{(=~)}操作符的类型签名很难理解，因此这里暂时不做解释。

\acode{=~}对它的两个参数和返回类型使用了 typeclasses。第一个参数（即\acode{=~}左侧）是要匹配的文本；第二个参数（右侧）则是要
匹配的正则表达式。我们可以传\acode{String}或\acode{ByteString}作为参数。

\subsubsection*{结果的多种类型}

\acode{=~}操作符的结果类型是多态的，因此 Haskell 编译器需要知道其类型。在真实代码中，它有可能根据使用的场景被推导出正确的类型。
但是\textbf{ghci}没法这么做，它没有足够的信息来进行推导。

当\textbf{ghci}不能推导\acode{target}类型时，我们需要进行显式指定：

\begin{lstlisting}[language=Haskell]
  ghci> "my left foot" =~ "foo" :: Bool
  Loading package array-0.1.0.0 ... linking ... done.
  Loading package containers-0.1.0.1 ... linking ... done.
  Loading package bytestring-0.9.0.1 ... linking ... done.
  Loading package mtl-1.1.0.0 ... linking ... done.
  Loading package regex-base-0.93.1 ... linking ... done.
  Loading package regex-posix-0.93.1 ... linking ... done.
  True
  ghci> "your right hand" =~ "bar" :: Bool
  False
  ghci> "your right hand" =~ "(hand|foot)" :: Bool
  True
\end{lstlisting}

正则表达式库的核心是一个名为\acode{RegexContext}的 typeclass，用于描述\acode{target}类型的行为；基础库为我们定义了很多实例。
\acode{Bool}类型就定义了该 typeclass 的实例，因此我们能得到可用的结果。另一个实例就是\acode{Int}：

\begin{lstlisting}[language=Haskell]
  ghci> "a star called henry" =~ "planet" :: Int
  0
  ghci> "honorificabilitudinitatibus" =~ "[aeiou]" :: Int
  13
\end{lstlisting}

如果寻求的是\acode{String}结果，我们将得到第一个匹配的子字符串，或是不匹配时得到一个空字符串。

\begin{lstlisting}[language=Haskell]
  ghci> "I, B. Ionsonii, uurit a lift'd batch" =~ "(uu|ii)" :: String
  "ii"
  ghci> "hi ludi, F. Baconis nati, tuiti orbi" =~ "Shakespeare" :: String
  ""
\end{lstlisting}

另外一个可作为结果的类型是\acode{[String]}，其返回\textit{所有}匹配字符串的列表：

\begin{lstlisting}[language=Haskell]
  ghci> "I, B. Ionsonii, uurit a lift'd batch" =~ "(uu|ii)" :: [String]

  <interactive>:1:0:
      No instance for (RegexContext Regex [Char] [String])
        arising from a use of `=~' at <interactive>:1:0-50
      Possible fix:
        add an instance declaration for
        (RegexContext Regex [Char] [String])
      In the expression:
              "I, B. Ionsonii, uurit a lift'd batch" =~ "(uu|ii)" :: [String]
      In the definition of `it':
          it = "I, B. Ionsonii, uurit a lift'd batch" =~ "(uu|ii)" ::
               [String]
  ghci> "hi ludi, F. Baconis nati, tuiti orbi" =~ "Shakespeare" :: [String]

  <interactive>:1:0:
      No instance for (RegexContext Regex [Char] [String])
        arising from a use of `=~' at <interactive>:1:0-54
      Possible fix:
        add an instance declaration for
        (RegexContext Regex [Char] [String])
      In the expression:
              "hi ludi, F. Baconis nati, tuiti orbi" =~ "Shakespeare" :: [String]
      In the definition of `it':
          it = "hi ludi, F. Baconis nati, tuiti orbi" =~ "Shakespeare" ::
               [String]
\end{lstlisting}

\begin{anote}
  注意字符串返回

  如果期望结果是一个\acode{String}，那么就要小心了。因为\acode{(=~)}返回一个空字符传来表示”没有匹配“，如果空字符串也可能是
  regexp 的有效匹配，那么就会难以识别。在这种情况下就应该是有一个不同的返回类型，例如\acode{[String]}。
\end{anote}

上述都是”简单的“返回类型，这远没有结束。继续往下推进前，让我们定义一个在之后例子中都会用到的匹配：

\begin{lstlisting}[language=Haskell]
  ghci> let pat = "(foo[a-z]*bar|quux)"
\end{lstlisting}

我们可以获得关于匹配发生的上下文的大量信息。如果期望是\acode{(String,String,String)}元组，则会在第一个匹配\textit{之前}
获得文本，匹配成功\textit{的}文本，以及\textit{紧随}其后的文本。

\begin{lstlisting}[language=Haskell]
  ghci> "before foodiebar after" =~ pat :: (String,String,String)
  ("before ","foodiebar"," after")
\end{lstlisting}

如果匹配失败，那么整个文本则会在元组中以”之前“元素作为返回，而其它两个元素为空：

\begin{lstlisting}[language=Haskell]
  ghci> "no match here" =~ pat :: (String,String,String)
  ("no match here","","")
\end{lstlisting}

请求一个四元素元组则会给我们第四个元素，它是匹配模式中所有组的列表：

\begin{lstlisting}[language=Haskell]
  ghci> "before foodiebar after" =~ pat :: (String,String,String,[String])
  ("before ","foodiebar"," after",["foodiebar"])
\end{lstlisting}

我们还可以获取匹配的数值信息。一对\acode{Int}给出的第一个匹配的起始偏移量以及长度。如果我们请求这些配对的列表，我们将得到所有
匹配的信息。

\begin{lstlisting}[language=Haskell]
  ghci> "before foodiebar after" =~ pat :: (Int,Int)
  (7,9)
  ghci> "i foobarbar a quux" =~ pat :: [(Int,Int)]

  <interactive>:1:0:
      No instance for (RegexContext Regex [Char] [(Int, Int)])
        arising from a use of `=~' at <interactive>:1:0-26
      Possible fix:
        add an instance declaration for
        (RegexContext Regex [Char] [(Int, Int)])
      In the expression: "i foobarbar a quux" =~ pat :: [(Int, Int)]
      In the definition of `it':
          it = "i foobarbar a quux" =~ pat :: [(Int, Int)]
\end{lstlisting}

如果请求单个元组，则用值\acode{-1}作为元组的第一个元素（匹配偏移量）表示匹配失败；如果请求元组列表，则用空列表表示匹配失败：

\begin{lstlisting}[language=Haskell]
  ghci> "eleemosynary" =~ pat :: (Int,Int)
  (-1,0)
  ghci> "mondegreen" =~ pat :: [(Int,Int)]

  <interactive>:1:0:
      No instance for (RegexContext Regex [Char] [(Int, Int)])
        arising from a use of `=~' at <interactive>:1:0-18
      Possible fix:
        add an instance declaration for
        (RegexContext Regex [Char] [(Int, Int)])
      In the expression: "mondegreen" =~ pat :: [(Int, Int)]
      In the definition of `it': it = "mondegreen" =~ pat :: [(Int, Int)]
\end{lstlisting}

\subsection*{更多的关于正则表达式}

\subsubsection*{混合与匹配字符串类型}

之前提到\acode{=~}操作符使用 typeclasses 作为其参数类型以及返回类型。可以使用\acode{String}或严格\acode{ByteString}
的值来进行正则表达式计算。

\begin{lstlisting}[language=Haskell]
  ghci> :module +Data.ByteString.Char8
  ghci> :type pack "foo"
  pack "foo" :: ByteString
\end{lstlisting}

可以尝试不同组合的\acode{String}与\acode{ByteString}：

\begin{lstlisting}[language=Haskell]
  ghci> pack "foo" =~ "bar" :: Bool
  False
  ghci> "foo" =~ pack "bar" :: Int
  0
  ghci> pack "foo" =~ pack "o" :: [(Int, Int)]

  <interactive>:1:0:
      No instance for (RegexContext Regex ByteString [(Int, Int)])
        arising from a use of `=~' at <interactive>:1:0-21
      Possible fix:
        add an instance declaration for
        (RegexContext Regex ByteString [(Int, Int)])
      In the expression: pack "foo" =~ pack "o" :: [(Int, Int)]
      In the definition of `it':
          it = pack "foo" =~ pack "o" :: [(Int, Int)]
\end{lstlisting}

不过需要注意的是，如果期望匹配结果中包含字符串值，那么所匹配的文本必须是同一类型的字符串：

\begin{lstlisting}[language=Haskell]
  ghci> pack "good food" =~ ".ood" :: [ByteString]

  <interactive>:1:0:
      No instance for (RegexContext Regex ByteString [ByteString])
        arising from a use of `=~' at <interactive>:1:0-25
      Possible fix:
        add an instance declaration for
        (RegexContext Regex ByteString [ByteString])
      In the expression: pack "good food" =~ ".ood" :: [ByteString]
      In the definition of `it':
          it = pack "good food" =~ ".ood" :: [ByteString]
\end{lstlisting}

上述例子中，使用\acode{pack}将\acode{String}转为了\acode{ByteString}。类型检查器可以接受是因为\acode{ByteString}在
结果类型中展示出来了。但是如果尝试获取一个\acode{String}，则不会成功：

\begin{lstlisting}[language=Haskell]
  ghci> "good food" =~ ".ood" :: [ByteString]

  <interactive>:1:0:
      No instance for (RegexContext Regex [Char] [ByteString])
        arising from a use of `=~' at <interactive>:1:0-20
      Possible fix:
        add an instance declaration for
        (RegexContext Regex [Char] [ByteString])
      In the expression: "good food" =~ ".ood" :: [ByteString]
      In the definition of `it':
          it = "good food" =~ ".ood" :: [ByteString]
\end{lstlisting}

我们可以通过将左侧的字符串类型与结果再次匹配来简单的修复这个问题：

\begin{lstlisting}[language=Haskell]
  ghci> "good food" =~ ".ood" :: [String]

  <interactive>:1:0:
      No instance for (RegexContext Regex [Char] [String])
        arising from a use of `=~' at <interactive>:1:0-20
      Possible fix:
        add an instance declaration for
        (RegexContext Regex [Char] [String])
      In the expression: "good food" =~ ".ood" :: [String]
      In the definition of `it': it = "good food" =~ ".ood" :: [String]
\end{lstlisting}

\subsubsection*{其它需要注意的事项}

查看 Haskell 库文档时，可以看到几个与 regexp 相关的模块。\acode{Text.Regex.Base}模块定义了所有其他 regexp 模块所遵循的
通用API。可以同时安装 regexp API 的多个实现。在编写本文时，GHC与一个实现\acode{Text.Regex.Posix}捆绑在一起。顾名思义，
这个包提供 POSIX regexp 语义。

\begin{anote}
  Perl 与 POSIX 正则表达式

  如果是从 Perl、Python 或 Java 等语言来学习 Haskell的，并且在其中一种语言中使用过正则表达式，那么应该意识到由
  \acode{Text.Regex.Posix}模块处理的 POSIX regexp 与 Perl 风格的\acode{regexp}在某些重要方面是不同的。
  以下是一些比较明显的区别。

  Perl regexp 引擎在匹配备选项时执行左偏匹配，而 POSIX 引擎选择最贪婪的匹配。这意味着给定一个正则表达式\acode{(foo|fo*)}
  和一个文本字符串\acode{foooooo}, perl 风格的引擎将给出\acode{foo}的匹配（最左边的匹配），而 POSIX 引擎将匹配整个字符串
  （最贪婪的匹配）。

  POSIX regexp 的语法不如 perl 风格的 regexp 统一。它们还缺乏 perl 风格的 regexp 提供的许多功能，例如零宽度断言和
  对贪婪匹配的控制。
\end{anote}

\subsection*{将一个 glob 模式转为一个正则表达式}

我们已经见识到了很多种将文本域正则表达式匹配的方法，让我们将注意力转回到 glob 模式。我们希望编写一个接受一个 glob 模式的函数，
将其表示形式返回为正则表达式。glob 模式和正则表达式两者都是 text 字符串，因此函数的类型就很明确了：

\begin{lstlisting}[language=Haskell]
  module GlobRegex
    ( globToRegex,
    )
  where

  import Text.Regex.Posix ((=~))

  globToRegex :: String -> String
\end{lstlisting}

我们生成的正则表达式必须是\textit{固定的}，使得它可以从同到尾的匹配字符串。

\begin{lstlisting}[language=Haskell]
  globToRegex cs = '^' : globToRegex' cs ++ "$"
\end{lstlisting}

由于\acode{String}就是\acode{[Char]}的别名，这里\acode{:}操作符将一个值（这里是\acode{^}字符）放到一个列表的头部，而
这个列表则是还未出现的\acode{globToRegex}函数所返回的。

\begin{anote}
  在定义一个值之前使用它

  Haskell 使用一个值或者函数时，并不需要它们被声明或者在一个源文件中定义。一个定义出现在第一个被使用的地方之后是很正常的。
  Haskell 编译器并不关心这一层面上的顺序。这使我们可以灵活的按照逻辑来构建代码，而不是遵循编译器编写者的顺序。

  Haskell 模块的编写者通常会使用这个灵活性来让“更加重要”的代码前置于源文件中。这正式我们编写\acode{globToRegex}函数以及其
  帮助函数的方式。
\end{anote}

有了正则表达式以后\acode{globToRegex'}函数则用作于大部分的翻译工作。以下使用 Haskell 的模式匹配来进行编码：

\begin{lstlisting}[language=Haskell]
  globToRegex' :: String -> String
  globToRegex' "" = ""
  globToRegex' ('*' : cs) = ".*" ++ globToRegex' cs
  globToRegex' ('?' : cs) = '.' : globToRegex' cs
  globToRegex' ('[' : '!' : c : cs) = "[^" ++ c : charClass cs
  globToRegex' ('[' : c : cs) = '[' : c : charClass cs
  globToRegex' ('[' : _) = error "unterminated character class"
  globToRegex' (c : cs) = escape c ++ globToRegex' cs
\end{lstlisting}

第一个子句规定了如果达到了 glob 模式的末尾（到那时会看到空字符串），返回\acode{\$}，即正则表达式中的“匹配到了末尾”。接下来的
是的子句则是一系列从 glob 语义切换到正则表达语义的模式。最后一个子句处理其它的每个字符，转义优先。

\acode{escape}函数确保正则引擎不会解释某些特定字符：

\begin{lstlisting}[language=Haskell]
  escape :: Char -> String
  escape c
    | c `elem` regexChars = '\\' : [c]
    | otherwise = [c]
    where
      regexChars = "\\+()^$.{}]|"
\end{lstlisting}

\acode{charClass}帮助函数用于检测字符是否正确的终止，不修改输入直到遇到\acode{]}：

\begin{lstlisting}[language=Haskell]
  charClass :: String -> String
  charClass (']' : cs) = ']' : globToRegex' cs
  charClass (c : cs) = c : charClass cs
  charClass [] = error "unterminated character class"
\end{lstlisting}

我们已经定义好了\acode{globToRegex}与其帮助函数们，现在加载进\textbf{ghci}中测试一下：

\begin{lstlisting}[language=Haskell]
  ghci> :set -package regex-posix
  package flags have changed, resetting and loading new packages...
  ghci> :l GlobRegex.hs
  [1 of 1] Compiling GlobRegex        ( GlobRegex.hs, interpreted )
  Ok, one module loaded.
  ghci> globToRegex "f??.c"
  "^f..\\.c$"
\end{lstlisting}

看起来像是一个合理的正则，那么用它来匹配字符串呢？

\begin{lstlisting}[language=Haskell]
  ghci> "foo.c" =~ globToRegex "f??.c" :: Bool
  True
  ghci> "test.c" =~ globToRegex "t[ea]s*" :: Bool
  True
  ghci> "teste.txt" =~ globToRegex "t[ea]s*" :: Bool
  True
\end{lstlisting}

正常工作！我们还可以为\acode{fnmatch}创建一个临时定义：

\begin{lstlisting}[language=Haskell]
  ghci> let fnmatch pat name = name =~ globToRegex pat :: Bool
  ghci> :type fnmatch
  fnmatch
    :: rgx-bs-0.94.0.2-01560e7d:Text.Regex.Base.RegexLike.RegexLike
         Text.Regex.Posix.Wrap.Regex source1 =>
       String -> source1 -> Bool
  ghci> fnmatch "d*" "myname"
  False
\end{lstlisting}

\acode{fnmatch}并不符合“Haskell 本性”，通常 Haskell 风格的函数都需要描述性的“驼峰”名称，例如“file name matches”
为\acode{fileNameMatches}，那么我们的库可以这样命名：

\begin{lstlisting}[language=Haskell]
  matchesGlob :: FilePath -> String -> Bool
  name `matchesGlob` pat = name =~ globToRegex pat
\end{lstlisting}

\subsection*{一个重要的旁白：编写惰性函数}

在一个命令式的语言中，\acode{globToRegex'}函数通常会表述为一个循环。例如 Python 的标准 fnmatch 模块中包含了一个名为
\acode{translate}的函数，其功能与我们的\acode{globToRegex}函数一致。它的实现就是循环。

如果你有别的函数式编程的经验例如 Scheme 或 ML，那么肯定有“模仿循环的方法就是通过尾递归”这个概念。

再来看一下\acode{globToRegex'}函数，并不是一个尾递归函数。可以测试一下最后的子句（其它的子句结构都类似）。

\begin{lstlisting}[language=Haskell]
  globToRegex' (c : cs) = escape c ++ globToRegex' cs
\end{lstlisting}

它递归的应用了自身，且递归的结果作为一个参数在\acode{(++)}函数上。由于递归的应用\textit{并非}作为函数的最后处理部分，那么
\acode{globToRegex'}并不是一个尾递归。

那么为什么这个函数并非尾递归呢？答案就在 Haskell 的非严格计算策略。在提及这个话题之前，让我们快速的了解为什么在传统语言中，
我们需要避免这样的尾递归定义。以下是\acode{(++)}操作符的一个简单定义。它属于递归，但不是尾递归。

\begin{lstlisting}[language=Haskell]
  (++) :: [a] -> [a] -> [a]

  (x:xs) ++ ys = x : (xs ++ ys)
  []     ++ ys = ys
\end{lstlisting}

在严格执行语言中，如果我们计算\acode{"foo" ++ "bar"}，整个列表会被构建出来，接着再返回。非严格计算则会延迟直到真正需要被
用到的时候。

如果需要表达式\acode{"foo" + "bar"}的一个元素时，函数定义的第一个模式将被匹配，接着返回表达式\acode{x : (xs ++ ys)}。
因为\acode{(:)}构造函数是非严格的，那么\acode{xs ++ ys}的计算就能被递延：按需生产更多的元素。当我们生产更多的结果时，我们
将不再使用\acode{x}，因此垃圾回收器将会进行回收。由于是按需生产元素，且不持有已经完成的部分，编译器就可以以常量的空间来计算
我们的代码。

\subsection*{使用我们的模式匹配器}

通过一个函数用于匹配 glob 模式是很好的，但是我们希望将其用于实际应用。在 Unix 系的系统中\acode{glob}函数返回所有匹配到的
文件名称以及路径。让我们在 Haskell 中也构建一个类似的函数：

\begin{lstlisting}[language=Haskell]
  module Glob (namesMatching) where
\end{lstlisting}

\acode{System.Directory}模块提供了用于处理路径以及内容的标准函数。

\begin{lstlisting}[language=Haskell]
  import System.Directory (doesDirectoryExist, doesFileExist, getCurrentDirectory, getDirectoryContents)
\end{lstlisting}

而\acode{System.FilePath}模块抽象了操作系统路径名称转换的细节，\acode{(</>)}函数将两个路径连接在一起：

\begin{lstlisting}[language=Haskell]
  ghci> :m +System.FilePath
  ghci> "foo" </> "bar"
  "foo/bar"
\end{lstlisting}

\acode{dropTrailingPathSeparator}函数正如其名，去除末尾的路径分隔：

\begin{lstlisting}[language=Haskell]
  ghci> dropTrailingPathSeparator "foo/"
  "foo"
\end{lstlisting}

\acode{splitFileName}则分离了路径与文件：

\begin{lstlisting}[language=Haskell]
  ghci> splitFileName "foo/bar/Quux.hs"
  ("foo/bar/","Quux.hs")
\end{lstlisting}

\acode{System.Directory}模块结合\acode{System.FilePath}的使用，我们可以编写一个同时作用于 Unix 系与 Windows 的
\acode{namesMatching}函数：

\begin{lstlisting}[language=Haskell]
  import System.FilePath (dropTrailingPathSeparator, splitFileName, (</>))
\end{lstlisting}

在这个模块中，我们将模仿一个“for”循环；首次在 Haskell 中尝试错误处理；并使用写好的\acode{matchesGlob}函数。

\begin{lstlisting}[language=Haskell]
  import Control.Exception (handle)
  import Control.Monad (forM)
  import GlobRegex (matchesGlob)
\end{lstlisting}

由于路径与文件存在于拥有副作用的“真实世界”，我们的 globbing 函数的返回值则必须带上\acode{IO}。

如果传入的字符串没有包含模式字符，那么就简单的检查给定的名称是否存在于文件系统重。（注意，我们在这里使用 Haskell 的函数守护
语法来编写一个漂亮整洁的定义。使用“if”也是可以的，不过就没有这么美观了）

\begin{lstlisting}[language=Haskell]
  isPattern :: String -> Bool
  isPattern = any (`elem` "[*?")

  namesMatching pat
    | not (isPattern pat) = do
        exists <- doesNameExist pat
        return ([pat | exists])
\end{lstlisting}

函数\acode{doesNameExist}会在稍后进行定义。

那么如果字符串\textit{是}一个 glob 模式呢？继续我们的函数定义：

\begin{lstlisting}[language=Haskell]
  | otherwise = do
      case splitFileName pat of
        ("", baseName) -> do
          curDir <- getCurrentDirectory
          listMatches curDir baseName
        (dirName, baseName) -> do
          dirs <-
            if isPattern dirName
              then namesMatching (dropTrailingPathSeparator dirName)
              else return [dirName]
          let listDir =
                if isPattern baseName
                  then listMatches
                  else listPlain
          pathNames <- forM dirs $ \dir -> do
            baseNames <- listDir dir baseName
            return (map (dir </>) baseNames)
          return (concat pathNames)
\end{lstlisting}

我们使用了\acode{splitFileName}将字符串分离成了一对“所有东西除了最终名称”以及“最终名称”。如果首个元素为空，则在当前路径寻找
一个模式；否则检查路径名称是否包含模式，没有包含则创建一个包含路径名称的单例列表，包含则列举所有匹配的路径。

\begin{anote}
  需要注意的事情

  \acode{System.FilePath}模块可以变得有点棘手。上述的例子中\acode{splitFileName}函数留下了末尾的斜杠

  \begin{lstlisting}[language=Haskell]
    ghci> :module +System.FilePath
    ghci> splitFileName "foo/bar"
    ("foo/","bar")
  \end{lstlisting}

  如果我们忘了（或者不了解）要移除斜杠，我们则会无限在\acode{namesMatching}中递归，因为下列行为

  \begin{lstlisting}[language=Haskell]
    ghci> splitFileName "foo/"
    ("foo/","")
  \end{lstlisting}

  你可以猜想一下之后将会发生什么！
\end{anote}

最后，收集了所有匹配的路径，得到了一个列表的列表，再将它们打平成一个单独的列表。

这里陌生的\acode{forM}函数其行为类似于一个“for”循环：它映射它的第二个参数（一个 action）在第一个参数（一个列表）上，并返回
一个列表的结果。

还剩下一些需要处理的问题。首先是\acode{doesNameExist}函数。\acode{System.Directory}模块并不能检查一个名称是否存在与文件
系统中，它强制要求我们决定检查的是文件还是路径。这个 API 很丑陋，因此我们需要将两者合二为一。首先检查是否为文件，因为文件相较于
路径更为普遍。

\begin{lstlisting}[language=Haskell]
  doesNameExist :: FilePath -> IO Bool
  doesNameExist name = do
    fileExists <- doesFileExist name
    if fileExists
      then return True
      else doesDirectoryExist name
\end{lstlisting}

还有另外两个函数需要定义，它们的返回都是一个路径中的所有名称。\acode{listMatches}函数返回的是一个路径中匹配了给定 glob 模式
的所有文件。

\begin{lstlisting}[language=Haskell]
  listMatches :: FilePath -> String -> IO [String]
  listMatches dirName pat = do
    dirName' <-
      if null dirName
        then getCurrentDirectory
        else return dirName
    handle errorHandler $ do
      names <- getDirectoryContents dirName'
      let names' =
            if isHidden pat
              then filter isHidden names
              else filter (not . isHidden) names
      return (filter (`matchesGlob` pat) names')
    where
      errorHandler :: SomeException -> IO [String]
      errorHandler _ = return []

  isHidden :: [Char] -> Bool
  isHidden ('.' : _) = True
  isHidden _ = False
\end{lstlisting}

与原文不同之处在于\acode{handle errorHandler}这里需要显式的为\acode{errorHandler}标注类型，否则原文的
\acode{handle (const (return []))}将会在编译时抛出类型不明确的异常，
详见\href{https://stackoverflow.com/a/6009807/8163324}{StackOverflow}。

\acode{listPlain}函数的返回要么是一个空的列表要么是一个单例列表，这取决于传递的名称是否存在：

\begin{lstlisting}[language=Haskell]
  listPlain :: FilePath -> String -> IO [String]
  listPlain dirName baseName = do
    exists <-
      if null baseName
        then doesDirectoryExist dirName
        else doesNameExist (dirName </> baseName)
    return ([baseName | exists])
\end{lstlisting}

\subsection*{通过 API 设计来处理错误}

如果传递给我们的\acode{globToRegex}的是一个错误的模式，并不会是一场灾难。这有可能使用户输入了错误的模式，这种情况下我们需要
报告有意义的错误信息。

\begin{lstlisting}[language=Haskell]
  type GlobError = String

  globToRegex :: String -> Either GlobError String
\end{lstlisting}

\subsection*{运行我们的代码}

\acode{namesMatching}函数并不能很好地单独工作，我们需要将其与其它函数结合。

首先定义一个\acode{renameWith}函数，并非只是简单的为一个文件重命名，而是将一个函数应用至文件名称，用函数返回的值来重命名文件。

\begin{lstlisting}[language=Haskell]
  import System.FilePath (replaceExtensions)
  import System.Directory (doesFileExist, renameDirectory, renameFile)
  import Glob (namesMatching)

  renameWith :: (FilePath -> FilePath) -> FilePath -> IO FilePath
  renameWith f path = do
    let path' = f path
    rename path path'
    return path'
\end{lstlisting}

这里还是需要一个帮助函数来处理文件与路径：

\begin{lstlisting}[language=Haskell]
  rename :: FilePath -> FilePath -> IO ()
  rename old new = do
    isFile <- doesFileExist old
    let f = if isFile then renameFile else renameDirectory
    f old new
\end{lstlisting}

\acode{System.FilePath}模块提供了很多操作文件名的函数，它们可以很好地与\acode{renameWith}与\acode{namesMatching}
函数结合，使得我们可以快速的使用它们来创造复杂行为的函数。例如修改 C++ 源文件的后缀。

\begin{lstlisting}[language=Haskell]
  cc2cpp :: IO [FilePath]
  cc2cpp = mapM (renameWith (flip replaceExtension ".cpp")) =<< namesMatching "*.cc"
\end{lstlisting}

\acode{flip}函数接受另一个函数作为参数，交换该函数的入参。\acode{=<<}函数将其右侧 action 的结果喂给左侧的 action。

\end{document}
