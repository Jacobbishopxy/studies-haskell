\documentclass[./main.tex]{subfiles}

\begin{document}

\subsection*{Haskell 中的经典 I/O}

略

\subsection*{处理文件以及句柄}

Haskell 为 I/O 定义了相当多的基本函数，其中许多函数与其它编程语言中的函数相似。\acode{System.IO}库中提供了所有的基础 I/O 函数。

使用\acode{openFile}会返回一个文件的\acode{Handle}，它用于对文件执行指定的操作。Haskell 提供了例如\acode{hPutStrLn}这样的
函数，其类似于\acode{putStrLn}，不同在于接受一个额外的参数 -- 一个\acode{Handle} -- 指定哪个文件被操作。当我们结束时，需要用
\acode{hClose}来结束\acode{Handle}。这些函数都定义在\acode{System.IO}中，“h” 开头的函数对应了几乎所有的非 “h” 开头的函数；
例如\acode{print}打印在屏幕上，而\acode{hPrint}打印至一个文件。

一个例子：

\begin{lstlisting}[language=Haskell]
  import Data.Char (toUpper)
  import System.IO

  main :: IO ()
  main = do
    inh <- openFile "input.txt" ReadMode
    outh <- openFile "output.txt" WriteMode
    mainloop inh outh
    hClose inh
    hClose outh

    putStrLn "whatever"

  mainloop :: Handle -> Handle -> IO ()
  mainloop inh outh = do
    ineof <- hIsEOF inh
    if ineof
      then return ()
      else do
        inpStr <- hGetLine inh
        hPutStrLn outh (map toUpper inpStr)
        mainloop inh outh
\end{lstlisting}

\acode{mainloop}首先检查输入是否结束（EOF），如果没有则读取一行，将该行转为大写后写入输出文件中，再递归的调用\acode{mainloop}。

略

\begin{center}
  \begin{tabular}{|l|l|l|l|l|}
    \hline
    \multicolumn{5}{|c|}{\textbf{可能的 IOMode 值}}                                                            \\
    \hline
    IOMode        & \textbf{可读？} & \textbf{可写？} & \textbf{起始位置} & \textbf{说明}                      \\
    \hline
    ReadMode      & Yes             & No              & 文件起始          & 文件必须存在                       \\
    WriteMode     & No              & Yes             & 文件起始          & 文件存在时会被清空                 \\
    ReadWriteMode & Yes             & Yes             & 文件起始          & 文件不存在时创建；否则现有数据保留 \\
    AppendMode    & No              & Yes             & 文件尾部          & 文件不存在时创建；否则现有数据保留 \\
    \hline
  \end{tabular}
\end{center}

\subsubsection*{关闭句柄}

上述例子中我们已经见到了\acode{hClose}用于关闭文件的句柄。在之后的小节中我们将了解缓存 Buffering 这个概念，即 Haskell 在内部
为文件维护了缓存。这样做能显著的提升性能，然而这就意味着在对打开的文件调用\acode{hClose}之前，数据可能不会被刷新到操作系统中。

另一个原因是打开着的文件消耗系统的资源。如果程序运行的时间很长，开开了很多文件但是并没有关闭它们，那么程序很可能会因为资源枯竭而
导致崩溃。

\subsubsection*{Seek 与 Tell}

从一个关联了磁盘文件的\acode{Handle}读写时，操作系统会维护一个关于当前位置的内部记录。每当进行下一层读取的时，操作系统返回下一个
数据块，其起始点为当前位置，且增加的位置反应了读取了多少数据。

你可以使用\acode{hTell}来找到文件中当前的位置。当文件被创建时，它是空的且你的位置将会是 0。在编写 5 个字节后，你的位置则变为了
5，以此类推。\acode{hTell}接受一个\acode{Handle}并返回一个\acode{IO Integer}来表示你的位置。

\acode{hTell}的同伴是\acode{hSeek}，它允许你更改文件的位置，其三个参数为：\acode{Handle}，\acode{SeekMode}以及一个位置。

\acode{SeekMode}有三个不同类型，用于指定如何解析给定的位置。\acode{AbsoluteSeek}为文件中的精确位置，等同于\acode{hTell}；
\acode{RelativeSeek}意为当前位置开始的多少位置，正值向后，负值向前；\acode{SeekFromEnd}则是从文件末尾往前多少的位置。

并不是所有的\acode{Handle}都是可以 seek 的。一个\acode{Handle}通常关联一个文件，但是它还可以关联其它的东西，比如网络连接，
磁带机，或者终端。可以使用\acode{hIsSeekable}来查看给定的\acode{Handle}是否可以 seek。

\subsubsection*{标准输入，输出以及错误}

较早之前我们指出每个非“h”函数通常都会有与其关联的“h”函数用作于任何\acode{Handle}上。实际上非“h”函数只不过是“h”函数的一种缩写。

在\acode{System.IO}中有三个预定义的\acode{Handle}：\acode{stdin}标准输入，通常是键盘；\acode{stdout}标准输出，通常是
显示器；\acode{stderr}标准错误，通常也是显示器。

像是\acode{getLine}类似的函数可以简单的定义成：

\begin{lstlisting}[language=Haskell]
  getLine = hGetLine stdin
  putStrLn = hPutStrLn stdout
  print = hPrint stdout
\end{lstlisting}

\subsubsection*{删除与文件重命名}

\acode{System.Directory}模块提供了两个比较有用的函数：\acode{removeFile}接受单个参数，即文件名，然后删除该文件；
\acode{renameFile}接受两个文件名，一个旧名称以及一个新的名称，如果新的文件名是一个不同的路径，那么可以认为这是一个移动。
旧的文件名必须在调用\acode{renameFile}之前就存在，另外如果新文件已经存在，则重命名后将其删除。

跟很多其它接受一个文件名的函数一样，如果“旧”名称不存在，\acode{renameFile}则会抛出异常。

\acode{System.Directory}模块中还有很多其他的函数，像是创建或移除文件夹，在路径中查找文件列表，测试文件是否存在，等等。

\subsubsection*{临时文件}

程序员频繁的需要临时文件。这些文件被用于存储大量等待计算的数据，可被其它程序所用的数据，等等。

通过名为\acode{openTempFile}的函数（以及相关联的\acode{openBinaryTempFile}）可以帮助我们解决不少问题。

\acode{openTempFile}接受两个参数：创建文件的路径，以及一个“template”用于命名文件。路径可以是\acode{.}来代表当前路径，或者
也可以使用\acode{System.Directory.getTemporaryDirectory}来找到操作系统所给出的最佳放置临时文件的位置。template 则将一些
随机字符添加至文件，用以确保结果是真正唯一的。实际上它保证了可以在一个唯一的文件名上工作。

\acode{openTempFile}的返回类型是\acode{IO (FilePath, Handle)}。元组的第一部分是被创建文件的名称，第二部分则是一个模式为
\acode{ReadWriteMode}的打开了文件的\acode{Handle}。当我们使用完文件，我们希望用\acode{hClose}来操作\acode{Handle}用于
关闭文件，接着调用\acode{removeFile}来删除它。接下来我们会看到一个例子用于展示这些函数的用法。

\subsection*{扩展案例：函数式 I/O 以及临时文件}



\begin{lstlisting}[language=Haskell]
  import Control.Exception
  import System.Directory (getTemporaryDirectory, removeFile)
  import System.IO

  main :: IO ()
  main = withTempFile "mytemp.txt" myAction

  myAction :: FilePath -> Handle -> IO ()
  myAction tempname temph = do
    -- Start by displaying a greeting on the terminal
    putStrLn "Welcome to tempfile.hs"
    putStrLn $ "I have a temporary file at " ++ tempname

    -- Let's see what the initial position is
    pos <- hTell temph
    putStrLn $ "My initial position is " ++ show pos

    -- Now, write some data to the temporary file
    let tempdata = show [1 .. 10]
    putStrLn $ "Writing one line containing " ++ show (length tempdata) ++ " bytes: " ++ tempdata
    hPutStrLn temph tempdata

    -- Get our new position. This doesn't actually modify pos in memory,
    -- but makes the name "pos" correspond to a different value for
    -- the remainder of the "do" block.
    pos <- hTell temph
    putStrLn $ "After writing, my new position is " ++ show pos

    -- Seek to the beginning of the file and display it
    putStrLn "The file content is: "
    hSeek temph AbsoluteSeek 0

    -- hGetContents performs a lazy read of the entire file
    c <- hGetContents temph

    -- Copy the file byte-for-byte to stdout, followed by \n
    putStrLn $ "c: " ++ c

    -- Let's also display it as a Haskell literal
    putStrLn "Which could be expressed as this Haskell literal:"
    print c

  {-
    This function takes two parameters: a filename pattern and another function.
    It will create a temporary file, and pass the name and Handle of that file to
    the given function.

    The temporary file is created with openTempFile. The directory is the one
    indicated by getTemporaryDirectory, or, if the system has no notion of a temporary
    directory, "." is used. The given pattern is passed to openTempFile.

    After the given function terminates, even if it terminates due to an exception,
    the Handle is closed and the file is deleted.
  -}
  withTempFile :: String -> (FilePath -> Handle -> IO a) -> IO a
  withTempFile pattern func = do
    -- The library ref says that getTemporaryDirectory may raise on exception on
    -- systems that have no notion of a temporary directory. So, we run
    -- getTemporaryDirectory under catch. catch takes two functions: one to run,
    -- and a different one to run if the first raised an exception.
    -- If getTemporaryDirectory raised an exception, just use "." (the current
    -- working directory).
    tempdir <-
      catch
        getTemporaryDirectory
        (\(_ :: IOException) -> return ".") -- explicit annotates exception type
    (tempfile, temph) <- openTempFile tempdir pattern

    -- Call (func tempfile temph) to perform the action on the temporary file.
    -- finally takes two actions. The first is the action to run, the second is an action
    -- to run after the first, regardless of the temporary file is always deleted.
    -- The return value from finally is the first action's return value.
    finally
      (func tempfile temph)
      ( do
          hClose temph
          removeFile tempfile
      )
\end{lstlisting}

首先\acode{withTempFile}函数证明了 Haskell 在 I/O 时也没有忘记其函数式的天性。该函数接受一个\acode{String}与另一个函数。
传递给\acode{withTempFile}的函数带着临时文件的\acode{Handle}被调用，当该函数退出时，临时文件关闭并被删除。

异常处理可以让程序变得更加健壮。通常我们希望在程序结束后删除临时文件，即使程序的执行过程中出现了错误。更多的错误处理会在第十九章
讲述。

现在回到程序，\acode{main}做的事情很简单，调用函数\acode{withTempFile}并传入临时文件名以及\acode{myAction}函数。

\acode{myAction}展示了一些信息到终端上，写入了一些数据到文件中，seek 到文件的起始位置，通过\acode{hGetContents}进行数据
读取，接着按照字节展示文件内容，同时通过\acode{print c}进行打印。这等同于\acode{putStrLn (show c)}。

现在看看输出：

\begin{lstlisting}[language=Bash]
  % runhaskell tempfiles.hs
  Welcome to tempfile.hs
  I have a temporary file at /var/folders/nb/w1q3ztlj139_vz9pqglmbks80000gn/T/mytemp3709-0.txt
  My initial position is 0
  Writing one line containing 22 bytes: [1,2,3,4,5,6,7,8,9,10]
  After writing, my new position is 23
  The file content is:
  c: [1,2,3,4,5,6,7,8,9,10]

  Which could be expressed as this Haskell literal:
  "[1,2,3,4,5,6,7,8,9,10]\n"
\end{lstlisting}

\subsection*{Lazy I/O}

Haskell 还提供了另一种方式处理 I/O。由于 Haskell 是 lazy 语言，意味着任何数据只会在其值必须被知道时才会被计算，那么就有了一些
新颖的 I/O 处理方法。

\subsubsection*{hGetContents}

一个新颖的 I/O 方式就是\acode{hGetContents}函数，其类型\acode{Handle -> IO String}，这里的\acode{String}代表着由文件的
\acode{Handle}所返回的所有数据。

在严格求值的语言中，使用这样的函数通常是一个坏主意。假设读一个 500GB 的文件，那么就有可能因为内存不足而导致程序崩溃。在这些语言中，
传统的做法就是使用循环来处理文件的所有数据。

但是\acode{hGetContents}不一样，它返回的\acode{String}是惰性的，即在调用\acode{hGetContents}时，并没有读取任何数据。
只有在处理列表的元素（字符）时才会从\acode{Handle}中读取数据。当\acode{String}的元素不再使用时，Haskell 的垃圾收集器则会自动
释放内存。

来看一个例子：

\begin{lstlisting}[language=Haskell]
  import Data.Char (toUpper)
  import System.IO

  main :: IO ()
  main = do
    inh <- openFile "input.txt" ReadMode
    outh <- openFile "output.txt" WriteMode
    inpStr <- hGetContents inh
    let result = processData inpStr
    hPutStr outh result
    hClose inh
    hClose outh

  processData :: String -> String
  processData = map toUpper
\end{lstlisting}

测试：

\begin{lstlisting}[language=Haskell]
  ghci> :load toupper-lazy1.hs
  [1 of 1] Compiling Main             ( toupper-lazy1.hs, interpreted )
  Ok, modules loaded: Main.
  ghci> processData "Hello, there!  How are you?"
  "HELLO, THERE!  HOW ARE YOU?"
  ghci> :type processData
  processData :: String -> String
  ghci> :type processData "Hello!"
  processData "Hello!" :: String
\end{lstlisting}

\begin{awarn}
  如果我们在上述例子中尝试在使用\acode{inpStr}的地方（对\acode{processData}的调用）之后继续使用\acode{inpStr}，那么程序
  则会失去其内存效率。这是因为编译器将会强制保留\acode{inpStr}值在内存中供未来使用。在这里编译器知道\acode{inpStr}不会再被
  使用，那么就在其使用结束后尽快的释放内存。这里需要记住：内存只有在最后一次使用后才会被释放。
\end{awarn}

这段代码有点繁琐，我们可以让它变得更简洁一些：

\begin{lstlisting}[language=Haskell]
  import Data.Char (toUpper)
  import System.IO

  main :: IO ()
  main = do
    inh <- openFile "input.txt" ReadMode
    outh <- openFile "output.txt" WriteMode
    inpStr <- hGetContents inh
    hPutStr outh (map toUpper inpStr)
    hClose inh
    hClose outh
\end{lstlisting}

在使用\acode{hGetContents}时，我们甚至不需要从输入文件中消费所有的数据。每当 Haskell 系统确定\acode{hGetContents}所返回的
整个字符可以被垃圾回收时 -- 意味着它不会再被使用 -- 文件将自动被关闭。同样的原则也适用于从文件中读取的数据。每当给定的数据不再
需要时，Haskell 环境就会释放存储该数据的内存。严格来说我们完全都不需要调用\acode{hClose}。然而这是一个好的习惯，因为之后对程序
的改动会使得\acode{hClose}变得很重要。

\begin{awarn}
  在使用\acode{hGetContents}时，必须记住即使在之后的程序可能不再显式的引用\acode{Handle}，但还是要保持\acode{Handle}
  不被关闭，直到\acode{hGetContents}的结果被消费掉了。不这么做的话就可能会导致丢失部分或者全部文件数据。这是因为 Haskell
  是惰性的，所以通常可以假设只有在输出了涉及输入的计算结果之后才消费了输入。
\end{awarn}

\subsubsection*{readFile 与 writeFile}

Haskell 程序员经常使用\acode{hGetContents}作为筛选。从一个文件读取数据，处理数据，接着将数据写到别的地方。这非常的通用所以有了
一些快捷的方式：\acode{readFile}以及\acode{writeFile}就是这样的函数，以字符串的方式处理文件。它们处理了所有关于打开文件，
关闭文件，读取数据，以及写入数据的细节。\acode{readFile}在内部使用了\acode{hGetContents}。

通过\textbf{ghci}查看这些函数的类型：

\begin{lstlisting}[language=Haskell]
  ghci> :type readFile
  readFile :: FilePath -> IO String
  ghci> :type writeFile
  writeFile :: FilePath -> String -> IO ()
\end{lstlisting}

使用\acode{readFile}与\acode{writeFile}改造之前的代码：

\begin{lstlisting}[language=Haskell]
  import Data.Char (toUpper)

  main :: IO ()
  main = do
    inpStr <- readFile "input.txt"
    writeFile "output.txt" $ map toUpper inpStr
\end{lstlisting}

\subsubsection*{关于 Lazy Output}

现在我们能理解惰性输入是如何在 Haskell 中工作的。那么惰性输出又是怎么样呢？

我们知道 Haskell 中所有计算都是在其值被需要时才会进行。由于函数例如\acode{writeFile}以及\acode{putStr}会输出所有传给它们的
\acode{String}，那么整个\acode{String}必须被计算。因此可以保证\acode{putStr}的参数将被完整求值。

那么输入惰性的意义呢？上述例子中，对\acode{putStr}或\acode{writeFile}的调用是否会强制将整个输入字符串立刻加载到内存中，仅仅
只是为了输出？

回答是不。\acode{putStr}（以及其它类似的所有输出函数）在数据可同时，输出数据。它们同样也不需要保留已经输出了的数据，因此只要程序
中没有其它东西需要它，内存就可以立刻释放。在某种意义上，可以将\acode{readFile}与\acode{writeFile}之间的\acode{String}视为
连接两者的管道。数据进入一端，以某种方式转换后，从另一端流出。

\subsubsection*{interact}

通过\acode{interact}函数可以进一步简化我们的程序：

\begin{lstlisting}[language=Haskell]
  import Data.Char (toUpper)

  main :: IO ()
  main = interact $ map toUpper
\end{lstlisting}

仅用了一行！测试：

\begin{lstlisting}[language=Haskell]
  $ runghc toupper-lazy4.hs < input.txt > output.txt
\end{lstlisting}

或者想要打印到屏幕上：

\begin{lstlisting}[language=Haskell]
  $ runghc toupper-lazy4.hs < input.txt
\end{lstlisting}

如果想要看到 Haskell 的输出确实是在接收到数据块后立刻输出到数据块，可以不加任何参数运行\acode{runghc toupper-lazy4.hs}，
这样就可以看到在敲击输入回车后，所有字符都会被立刻以大写的形式输出。

我们同样还可以通过\acode{interact}编写在输出前添加新行的程序：

\begin{lstlisting}[language=Haskell]
  import Data.Char (toUpper)

  main = interact $ map toUpper . (++) "Your data, in uppercase, is:\n\n"
\end{lstlisting}

由于我们在\acode{(++)}后调用的\acode{map}，那么新行也会变为大写，修改一下：

\begin{lstlisting}[language=Haskell]
  import Data.Char (toUpper)

  main = interact $ (++) "Your data, in uppercase, is:\n\n" . map toUpper
\end{lstlisting}

这就将新行移到\acode{map}外去了。

\subsubsection*{interact 加上过滤}

\acode{interact}的另一个常用方法就是过滤。比如打印带有字符“a”的每行：

\begin{lstlisting}[language=Haskell]
  main = interact $ unlines . filter (elem 'a') . lines
\end{lstlisting}

通过\textbf{ghci}查看上述的三个新函数：

\begin{lstlisting}[language=Haskell]
  ghci> :type lines
  lines :: String -> [String]
  ghci> :type unlines
  unlines :: [String] -> String
  ghci> :type elem
  elem :: (Eq a) => a -> [a] -> Bool
\end{lstlisting}

测试：

\begin{lstlisting}[language=Haskell]
  $ runghc filter.hs < input.txt
  I like Haskell
  Haskell is great
\end{lstlisting}

\subsection*{IO 单子}

\subsubsection*{Actions}

大多数语言不会区分纯函数和非纯函数。Haskell 的函数有其数学意义：它们是存粹的计算，且不会被任何外界的东西改变。另外，计算可以在
任何时刻进行。

那么就需要另一些工具来处理 I/O 了。这个工具在 Haskell 中叫做\textit{actions}。Actions 类似于函数，在定义的时候不会做任何
事情，而是在执行任务时才会被唤醒。I/O actions 被定义在\acode{IO}单子中。我们会在第十四章中讲解单子。看看一些类型：

\begin{lstlisting}[language=Haskell]
  ghci> :type putStrLn
  putStrLn :: String -> IO ()
  ghci> :type getLine
  getLine :: IO String
\end{lstlisting}

\acode{putStrLn}的类型跟其它函数无异。函数接受一个参数并返回一个\acode{IO ()}。这个\acode{IO ()}就是 action。我们可以
存储以及传递 actions 至纯代码中，尽管这并不常用。一个 action 不会做任何事情直到它被唤起。看看例子：

\begin{lstlisting}[language=Haskell]
  str2action :: String -> IO ()
  str2action input = putStrLn $ "Data: " ++ input

  list2actions :: [String] -> [IO ()]
  list2actions = map str2action

  numbers :: [Int]
  numbers = [1 .. 10]

  strings :: [String]
  strings = map show numbers

  actions :: [IO ()]
  actions = list2actions strings

  printitall :: IO ()
  printitall = runall actions

  -- Take a list of actions, and execute each of them in turn.
  runall :: [IO ()] -> IO ()
  runall [] = return ()
  runall (fst : remaining) = do
    fst
    runall remaining

  main :: IO ()
  main = do
    str2action "Start of the program"
    printitall
    str2action "Done!"
\end{lstlisting}

\acode{str2action}这个函数接受一个参数并返回一个\acode{IO ()}。在\acode{main}的结尾，可以在另一个 action 中直接使用它，
并打印出一行。或者可以存储 -- 而不是执行 -- 这个 action 到纯代码中。在\acode{list2actions}中 -- 使用了\acode{map}应用
在\acode{str2action}上并返回一个列表的 actions，正如我们在纯代码中的那样。可以看到通过\acode{printitall}的所有内容都是
使用纯的工具构建的。

尽管定义了\acode{printitall}，它不会被执行直到它的 action 在某处被计算。注意在\acode{main}中是如何将\acode{str2action}
作为一个 I/O action 被执行的，而之前我们在 I/O 单子外使用它，并将结果转为一个列表。

可以这么理解：每个声明，除了\acode{let}，在一个\acode{do}代码块中必须产生一个将要执行的 I/O action。

对\acode{printitall}的调用最终执行所有这些操作。实际上由于 Haskell 是惰性的，所以直到这里才会生成 actions。

测试：

\begin{lstlisting}[language=Haskell]
  % runhaskell actions.hs
  Data: Start of the program
  Data: 1
  Data: 2
  Data: 3
  Data: 4
  Data: 5
  Data: 6
  Data: 7
  Data: 8
  Data: 9
  Data: 10
  Data: Done!
\end{lstlisting}

实际上我们可以将其写成更紧凑的形式。以下是重构：

\begin{lstlisting}[language=Haskell]
  str2message :: String -> String
  str2message input = "Data: " ++ input

  str2action :: String -> IO ()
  str2action = putStrLn . str2message

  numbers :: [Int]
  numbers = [1 .. 10]

  main :: IO ()
  main = do
    str2action "Start of the program"
    mapM_ (str2action . show) numbers
    str2action "Done!"
\end{lstlisting}

注意\acode{str2action}中使用了标准函数组合操作符。在\acode{main}中，调用了\acode{mapM_}，该函数类似于\acode{map}
接受一个函数与一个列表。而提供给\acode{mapM_}的函数则是一个应用至列表中每个元素的 I/O action 。\acode{mapM_}会抛出函数的
结果，不过如果有需要，也可以使用\acode{mapM}返回 I/O 结果的列表。看一下它们的类型：

\begin{lstlisting}[language=Haskell]
  ghci> :type mapM
  mapM :: (Monad m) => (a -> m b) -> [a] -> m [b]
  ghci> :type mapM_
  mapM_ :: (Monad m) => (a -> m b) -> [a] -> m ()
\end{lstlisting}

\begin{anote}
  实际上这些函数并不仅仅作用于 I/O；它们可用于任何\acode{Monad}。
\end{anote}

为什么有了\acode{map}还需要\acode{mapM}呢？因为\acode{map}是一个返回列表的纯函数，它不能直接执行 actions，而\acode{mapM}
则是\acode{IO}单子中的工具，可用于实际执行 actions。

回到\acode{main}，\acode{mapM_}应用了\acode{(str2action . show)}在每个\acode{numbers}中的元素上。\acode{show}将每个
数值转为\acode{String}，而\acode{str2action}转换每个\acode{String}为一个 action。\acode{mapM_}结合这些独立的 actions
成为一个大的 action 并打印出来。

\subsubsection*{Sequencing}

\acode{do}代码块其实是合并所有 actions 的简便注解。在不使用\acode{do}时还有两个操作符可以使用：\acode{>>}与\acode{>>=}。

\begin{lstlisting}[language=Haskell]
  ghci> :type (>>)
  (>>) :: (Monad m) => m a -> m b -> m b
  ghci> :type (>>=)
  (>>=) :: (Monad m) => m a -> (a -> m b) -> m b
\end{lstlisting}

\acode{>>}操作符将两个 actions 串联起来：先执行第一个 action，再执行第二个 action。整个计算的结果则是第二个 action 的结果，
而第一个 action 的结果则被丢弃。这类似于\acode{do}代码块中的一行。可以用\acode{putStrLn "line 1" >> putStrLn "line 2"}
来测试一下。它将会打印出两行，将第一个\acode{putStrLn}的结果丢弃，将第二个的结果作为返回。

\acode{>>=}操作符运行一个 action，然后将其结果传给一个返回 action 的函数。第二个 action 也将运行，整个表达式的结果还是第二个
action 的结果。例如，可以用\acode{getLine >>= putStrLn}测试，它从键盘读取一行然后再将其输出。

现在不用\acode{do}代码块来重写一下之前的例子：

\begin{lstlisting}[language=Haskell]
  main :: IO ()
  main = do
    putStrLn "Greetings! What is your name?"
    inpStr <- getLine
    putStrLn $ "Welcome to Haskell, " ++ inpStr ++ "!"
\end{lstlisting}

不使用\acode{do}：

\begin{lstlisting}[language=Haskell]
  main :: IO ()
  main =
    putStrLn "Greetings! What is your name?"
      >> getLine
      >>= (\inpStr -> putStrLn $ "Welcome to Haskell, " ++ inpStr ++ "!")
\end{lstlisting}

在定义\acode{do}代码块时，Haskell 编译器内部会执行类似的转换。

\subsubsection*{Return 的本质}

在本章开头我们提到了\acode{return}可能并不像是它长得那样。很多语言的关键字\acode{return}是立刻终结函数的执行并返回一个值给到
调用者。

Haskell 中的\acode{return}函数则大相径庭。Haskell 中的\acode{return}将数据包装进单子中。在谈及 I/O 时，\acode{return}
用于将纯数据放入 IO 单子中。

为什么要这么做呢？记住，任何结果依赖于 I/O 的东西都必须在 IO 单子内。因此，如果我们正在编写一个执行 I/O 的函数，那么一个纯计算，
我们需要使用\acode{return}来使这个纯计算成为函数的正确返回值。例如：

\begin{lstlisting}[language=Haskell]
  import Data.Char (toUpper)

  isGreen :: IO Bool
  isGreen = do
    putStrLn "Is green your favorite color?"
    inpStr <- getLine
    return $ (toUpper . head $ inpStr) == 'Y'
\end{lstlisting}

我们有一个返回\acode{Bool}的纯计算。计算传递给\acode{return}，将其放入\acode{IO}单子内。由于它是\acode{do}代码块中的最后
一个值，它变成了\acode{isGreen}的返回值，但这并不因为我们使用了\acode{return}函数。

下面是同样一个程序，只不过将纯计算剥离成为一个独立的函数。这样可以隔离纯函数，同样使得意图更加的清晰。

\begin{lstlisting}[language=Haskell]
  import Data.Char (toUpper)

  isYes :: String -> Bool
  isYes inpStr = (toUpper . head $ inpStr) == 'Y'

  isGreen :: IO Bool
  isGreen = do
    putStrLn "Is green your favorite color?"
    isYes <$> getLine
\end{lstlisting}

与原文不同之处在于使用了\acode{<\$>}，即\acode{fmap}函数，替换掉了之前的\acode{inpStr <- getLine}以及\acode{return}。

最后是一个做作的例子用于展示\acode{return}并不需要位于\acode{do}代码块的结尾。

\begin{lstlisting}[language=Haskell]
  returnTest :: IO ()
  returnTest =
      do one <- return 1
         let two = 2
         putStrLn $ show (one + two)
\end{lstlisting}

\subsection*{Haskell 真的是命令式的吗？}

这些\acode{do}代码块看起来像是命令式的语言。毕竟大多数情况下我们都是在按顺序运行命令。

但是 Haskell 本质上仍然是一种惰性语言。虽然有时有必要对 I/O 操作进行排序，但这是使用了 Haskell 中的工具来完成的。Haskell 还
通过 IO 单子实现了 I/O 与语言其他部分的良好分离。

\subsection*{Lazy I/O 的副作用}

本章提到的\acode{hGetContents}，其返回的\acode{String}可以用在纯代码中。

我们需要更具体的了解副作用是什么。当我们说 Haskell 没有副作用时，这到底是什么意思？

某种层度上，副作用总是可能的。一个没有优化的循环，即使是用纯代码写的，也有可能导致系统的内存耗尽然后导致程序崩溃。或者它可能导致
数据被交换到磁盘。

当我们提到没有副作用，意思其实是 Haskell 的纯代码不会运行能触发副作用的命令。纯函数不会修改一个全局变量，I/O 请求，或者运行一个
命令关闭系统。

当你有一个\acode{hGetContents}而来的字符串被传递至一个纯函数时，函数不会知道\acode{String}是从磁盘文件而来的。它只会表现得
跟通常那样，但处理该\acode{String}可能会导致环境发出 I/O 命令。纯函数不会这么做；它们是纯函数处理的结果，就像将内存交换到磁盘的
例子一样。

在某些情况下，你可能需要更多的控制 I/O 发生的确切时间。也许是正在交互式的从用户那里读取数据，或者通过管道从另一个程序读取数据，并
需要直接与用户通信。这些情况下，\acode{hGetContents}可能不合适。

\subsection*{缓存}

\subsubsection*{缓存模式}

Haskell 有三种不同的缓存模式：\acode{BufferMode}类型：\acode{NoBuffering}，\acode{LineBuffering}以及
\acode{BlockBuffering}。

\acode{NoBuffering}正如其名 -- 无缓存。通过例如\acode{hGetLine}等函数读取的数据将每次从操作系统重读取一个字符。写入的数据
将立刻写入，也经常一次写入一个字符。通常\acode{NoBuffering}的性能很差，不适合通用用途。

\acode{LineBuffering}使输出缓冲区在输出换行符或缓存过大时被写入。在输入时，它通常会尝试读取任何可用的数据块，直到它第一次看到
换行符。当从终端读取数据时，每次按下回车键后应该立刻返回数据。这通常是合理的默认值。

\acode{BlockBuffering}使 Haskell 在可能的情况下以固定大小块来读写数据。在批量处理大量数据时，这是性能最好的方法，即使这些
数据是以行记录的。然而它不能用于交互式程序，因为它会阻塞输入，直到读取到完整的块。\acode{BlockBuffering}接受\acode{Maybe}
类型的参数：如果\acode{Nothing}，它将使用定义好的缓存大小；或者你可以使用\acode{Just 4096}之类的设置将缓存设置为 4096
字节。

默认和缓存模式取决于操作系统和 Haskell 的实现。可以通过调用\acode{hGetBuffering}向系统询问当前的缓存模式。当前的缓存模式
可以通过\acode{hSetBuffering}设置，它接受\acode{Handle}和\acode{BufferMode}。
例如\acode{hSetBuffering stdin (BlockBuffering Nothing)}。

\subsubsection*{刷新缓存}

对于任何类型的缓存，有时我们会希望强制 Haskell 写出保存在缓存中的任何数据。有些时候这会自动发生：比如说调用\acode{hClose}时。
而调用\acode{hFlush}将强制立刻写入任何挂起的数据。当\acode{Handle}是一个网络 socket 且想要立刻传输数据时，或者是想要将
磁盘上的数据提供给可能并发读取它的其它程序时，这会很有用。

\subsection*{读取命令行参数}

\acode{System.Environment.getArgs}以\acode{IO [String]}返回所有参数。类似 C 里的\acode{argv}，始于\acode{argv[1]}。
程序名（C 中的\acode{argv[0]}）则可以通过\acode{System.Environment.getProgName}得到。

\acode{System.Console.GetOpt}模块提供了一些解析命令行选项的工具。如果我们的程序有一些复杂的选项，该模块则很有帮助。

\subsection*{环境变量}

读取环境变量可以用\acode{System.Environment}的两个函数：\acode{getEnv}或\acode{getEnvironment}。前者查找特定变量，
变量不存在时抛出异常；后者将所有环境变量以\acode{[(String, String)]}输出，可以使用例如\acode{lookup}函数来找到所需的变量。

\end{document}
