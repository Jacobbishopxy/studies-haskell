\documentclass[./main.tex]{subfiles}

\begin{document}

本章我们将会使用第十章所编写的图像解析库来构建一个识别条形码的应用。

\subsection*{关于条形码}

绝大多数包装好的和批量生产的消费品上面都会有条形码。尽管在各个专门的领域都有若干的条形码系统，但消费品通常使用 UPC-A 或 EAN-13。前者是在美国
开发的，而后者则起源于欧洲。

EAN-13 开发晚于 UPC-A，它是 UPC-A 的超集。（实际上，UPC-A 已经在 2005 年宣布废弃了，尽管在美国它仍然被大量的使用。）任何可以识别 EAN-13
的软件或硬件也会自动处理 UPC-A 条形码。

正如其名，EAN-13 描述了一个 13 位数的序列，它被划分成四个组：

\begin{itemize}
  \item 开头的两位数描述了\textit{数字系统}。它可以表明制造商的国家，或是其它的一些分类，例如 ISBN（书籍身份）。
  \item 接下来的五位数则是生产商的 ID，通过国家的授权进行指定。
  \item 再接下来的五位数则是产品 ID，通过制造商指定。（小型的制造商可能有更长的制造商 ID 以及更短的产品 ID，但是它们加在一起仍然是十位数。）
  \item 最后一位则是\textit{校验数}，允许一个扫描器验证它扫描的位数字符串。
\end{itemize}

\subsubsection*{EAN-13 编码}

在我们关心 EAN-13 条形码的解码之前，我们需要理解它们是如何被编码的。EAN-13 所使用的系统有一点复杂。让我们首先从校验数开始，也就是字符串的
最后一个位数。

\begin{lstlisting}[language=Haskell]
  checkDigit :: (Integral a) => [a] -> a
  checkDigit ds = 10 - (sum products `mod` 10)
    where
      products = mapEveryOther (* 3) (reverse ds)

  mapEveryOther :: (a -> a) -> [a] -> [a]
  mapEveryOther f = zipWith ($) (cycle [f, id])
\end{lstlisting}

这是一种通过代码比口头描述更容易理解的算法。从字符串的右侧开始计算，每个连续的数字要么乘以 3，要么不动（\acode{cycle}函数会无限的重复它的输入
列表）。校验数则是它们的和（以 10 取模）与数字 10 之间的差。

条形码是一系列固定宽度的条码，黑色代表二元 “1” 位，白色代表 “0” 位。因此，相同数字的运行看前来像一个更粗的条码。

条形码中的位序列如下所示：

\begin{itemize}
  \item 前导保护序列，编码为 101.
  \item 一组六位数字，每个有七位宽。
  \item 另一个保护序列，编码为 01010.
  \item 一组六位以上的数字。
  \item 尾部的保护序列，编码为 101。
\end{itemize}

左边和右边组中的数字有单独的编码。左边用奇偶校验位编码，奇偶校验位编码条形码的第 13 位。

\subsection*{数组介绍}

在继续之前，以下是本章所需的模块导入：

\begin{lstlisting}[language=Haskell]
  import Control.Applicative ((<$>))
  import Control.Monad (forM_)
  import Data.Array (Array (..), bounds, elems, indices, ixmap, listArray, (!))
  import Data.ByteString.Lazy.Char8 qualified as L
  import Data.Char (digitToInt)
  import Data.Ix (Ix (..))
  import Data.List (foldl', group, sort, sortBy, tails)
  import Data.Map qualified as M
  import Data.Maybe (catMaybes, listToMaybe)
  import Data.Ratio (Ratio)
  import Data.Word (Word8)
  import Parse                                -- from Chapter 10
  import System.Environment (getArgs)
\end{lstlisting}

\textbf{注}：\acode{Parse}库从第十章而来。

条形码的编码过程很大程度上是表格驱动的，即使用位模式的小表来决定如何对每个数字进行编码。Haskell 基本的数据类型中列表和元组都不适合用于表格，
其包含的元素需要能被随机访问。列表需要线性的遍历才能访问第 k 个元素。元组没有这个问题，但是 Haskell 的类型系统使得很难编写一个函数来接受
元组和元素偏移量，并返回元组中偏移量处的元素。

常用的数据类型可供常数耗时的随机访问就是数组了。Haskell 提供了若干数字类型。这里我们将编码表格表达为字符串数组。

最简单的数组类型位于\acode{Data.Array}模块中，也正是我们现在所用的。与其它 Haskell 通常的类型一样，这些数组也都是不可变的。不可变数组
只有在创建时才能用值填充一次，其它时候是不能修改其内容的。（标准库同样提供了其它的数组类型，有些是可变的，只不过现在我们暂时还用不上。）

\begin{lstlisting}[language=Haskell]
  leftOddList =
    [ "0001101",
      "0011001",
      "0010011",
      "0111101",
      "0100011",
      "0110001",
      "0101111",
      "0111011",
      "0110111",
      "0001011"
    ]

  rightList = map complement <$> leftOddList
    where
      complement '0' = '1'
      complement '1' = '0'

  leftEvenList = map reverse rightList

  parityList =
    [ "111111",
      "110100",
      "110010",
      "110001",
      "101100",
      "100110",
      "100011",
      "101010",
      "101001",
      "100101"
    ]

  listToArray :: [a] -> Array Int a
  listToArray xs = listArray (0, l - 1) xs
    where
      l = length xs

  leftOddCodes, leftEvenCodes, rightCodes, parityCodes :: Array Int String
  leftOddCodes = listToArray leftOddList
  leftEvenCodes = listToArray leftEvenList
  rightCodes = listToArray rightList
  parityCodes = listToArray parityList
\end{lstlisting}

\acode{Data.Array}模块的\acode{listArray}函数将列表转为数组。第一个参数是数组所需的边界；第二个参数则是列表。

\acode{Array}的一个不寻常特征是，它的类型在它包含的数据和索引类型上都是参数化的。一维\acode{String}数组的类型是\acode{Array Int String}，
而二维则是\acode{Array (Int,Int) String}。

\begin{lstlisting}[language=Haskell]
  ghci> :m +Data.Array
  ghci> :t listArray
  listArray :: Ix i => (i, i) -> [e] -> Array i e
\end{lstlisting}

构造一个数组：

\begin{lstlisting}[language=Haskell]
  ghci> listArray (0,2) "foo"
  array (0,2) [(0,'f'),(1,'o'),(2,'o')]
\end{lstlisting}

注意我们指定了数组的上下界。这些界是包含边界的，即从 0 至 2 的数组包含了 0，1 以及 2。

\begin{lstlisting}[language=Haskell]
  ghci> listArray (0,3) [True,False,False,True,False]
  array (0,3) [(0,True),(1,False),(2,False),(3,True)]
  ghci> listArray (0,10) "too short"
  array (0,10) [(0,'t'),(1,'o'),(2,'o'),(3,' '),(4,'s'),(5,'h'),(6,'o'),(7,'r'),(8,'t'),(9,*** Exception: (Array.!): undefined array element
\end{lstlisting}

当数组被构建后，可以使用\acode{(!)}操作符根据索引来访问元素。

\begin{lstlisting}[language=Haskell]
  ghci> let a = listArray (0,14) ['a'..]
  ghci> a ! 2
  'c'
  ghci> a ! 100
  *** Exception: Ix{Integer}.index: Index (100) out of range ((0,14))
\end{lstlisting}

由于数组的构造函数允许指定数组的边界，我们无需使用类似于 C 语言那样基于零的边界。

\begin{lstlisting}[language=Haskell]
  ghci> let a = listArray (-9,5) ['a'..]
  ghci> a ! (-2)
  'h'
\end{lstlisting}

索引类型可以是任意\acode{Ix}类型的成员。例如\acode{Char}作为索引：

\begin{lstlisting}[language=Haskell]
  ghci> let a = listArray ('a','h') [97..]
  ghci> a ! 'e'
  101
\end{lstlisting}

创建一个高阶维度数组，我们可以使用\acode{Ix}实例的元组作为索引类型。以下是一个三维数组：

\begin{lstlisting}[language=Haskell]
  ghci> let a = listArray ((0,0,0),(9,9,9)) [0..]
  ghci> a ! (4,3,7)
  437
\end{lstlisting}

\subsubsection*{数组与惰性}

用于构造数组的列表，其元素数必须大于等于数组所需的元素。如果没有提供足够的元素则会在运行时抛出异常。该异常将何时出现取决于数组的具体实现。

这里我们使用的是一个非严格的数组类型。

\begin{lstlisting}[language=Haskell]
  ghci> let a = listArray (0,5) "bar"
  ghci> a ! 2
  'r'
  ghci> a ! 4
  *** Exception: (Array.!): undefined array element
\end{lstlisting}

Haskell 同样提供了严格数组，其行为就不同了。我们将在后续小节中讨论两者的优劣。

\subsubsection*{折叠数组}

\acode{bounds}函数返回一个描述了创建数组时的边界元组。\acode{indices}函数返回列表的每个索引。我们可以用其定义一些有用的折叠，因为
\acode{Data.Array}模块并没有定义任何折叠函数。

\begin{lstlisting}[language=Haskell]
  -- Strict left fold, similar to foldl' on lists
  foldA :: (Ix k) => (a -> b -> a) -> a -> Array k b -> a
  foldA f s a = go s (indices a)
    where
      go s (j : js) = let s' = f s (a ! j) in s' `seq` go s' js
      go s _ = s

  -- Strict left fold using the first element of the array as its starting value, similar to fold1 on lists
  foldA1 :: (Ix k) => (a -> a -> a) -> Array k a -> a
  foldA1 f a = foldA f (a ! fst (bounds a)) a
\end{lstlisting}

你可能会疑惑为什么数组模块并没有提供如此有用的折叠函数。这是因为一维数组与列表之间有一些显著的差异。例如自然而言，折叠有两个方向：从左向右，
从右向左。除此之外，每次只能折叠一个元素。

而对于二维的数组来说这根本行不通。首先，这时的折叠可以有多种方案。我们可能还是想每次只折叠单个元素，但是现在有了列折叠或行折叠的可能性。
最重要的是，对于一次一个元素的折叠，不再只有两个序列需要遍历。

换言之，对于二维数组，就存在若干可能的折叠方式组合，因此没有太多令人信服的理由为标准库选择少量排列。这个问题在高维情况下会更加复杂，因此最好
让开发人员编写适合自身应用需求的折叠。

\subsubsection*{修改数组元素}

对于不变数组而言，虽然存在“修改”函数，实际上并不常用。例如，\acode{accum}函数接受一个数组，以及一个\acode{(index, value)}对的列表，
返回一个新的被替换后的数组。

由于数组是不变的，修改一个元素就需要拷贝整个数组，这实际上是相当昂贵的。

另一个数组类型，\acode{Data.Array.Diff}模块中的\acode{DiffArray}，尝试在少量修改时减少消耗，但是在编写这本书的时候，它在实际运用中
还是太慢了。

\begin{anote}
  不要放弃希望

  在 Haskell 中实际上高效的修改一个数组是可能的，即通过\acode{ST}单子。我们将在第 26 章对其进行学习。
\end{anote}

\subsection*{对 EAN-13 条形码进行编码}

尽管我们的目标是\textit{解码}条形码，不过拥有一个编码器作为引用是很有帮助的。例如通过\acode{decode . encode}来检查输出是否正确，确保
我们代码的正确性。

\begin{lstlisting}[language=Haskell]
  encodeEAN13 :: String -> String
  encodeEAN13 = concat . encodeDigits . map digitToInt

  -- this function computes the check digit; don't pass one in.
  encodeDigits :: [Int] -> [String]
  encodeDigits s@(first : rest) =
    outerGuard : lefties ++ centerGuard : righties ++ [outerGuard]
    where
      (left, right) = splitAt 5 rest
      lefties = zipWith leftEncode (parityCodes ! first) left
      righties = map rightEncode (right ++ [checkDigit s])

  leftEncode :: Char -> Int -> String
  leftEncode '1' = (leftOddCodes !)
  leftEncode '0' = (leftEvenCodes !)

  rightEncode :: Int -> String
  rightEncode = (rightCodes !)

  outerGuard = "101"

  centerGuard = "01010"
\end{lstlisting}

字符串的编码长度是十二位数字，通过\acode{encodeDigits}来添加第十三个校验位数字。

条形码由两组六位数字进行编码，由守护序列位于两组其中以及两侧。那么其它数字呢？

左侧组的每个数字通过奇数或偶数编码，奇偶的选择取决于字符串中的第一位数字。如果第一位为零，那么左侧组则是由偶数进行编码；为一，则是由奇数进行
编码。这种方式的编码很优雅，它使得 EAN-13 条形码变得向后兼容旧的 UPC-A 标准。

\subsection*{解码器的约束}

在讨论解码钱，需要为条形码可以工作的类别设定一些约束。

手机相机或者网络相机输出的是 JPEG 图像，但是编写一个 JPEG 解码器需要占用若干章节。因此我们通过处理 netpbm 文件格式简化解析问题。我们将使用
第十章节所开发的解析组合子。

\subsection*{分治法}

我们的任务是将一个有效的条形码从相机图片中提取出来。我们可以将这个大问题切分成几个有序的子问题，它们每个都是独立可追溯的。

\begin{itemize}
  \item 将颜色数据转换成可处理的数据。
  \item 从图像中采样一个扫描线，并提取一组猜测数据，用以确定这一行中的编码数字可能会是什么。
  \item 从猜测数据中创建一个有效的解码列表。
\end{itemize}

更多的子问题将会在后续进行分割。

\subsection*{将彩色图像转换成易于处理的东西}

条形码实际上是黑白条的序列，我们希望编写一个简单的解析器，一个简单的表示是单色图像，其中每个像素不是黑色就是白色。

\subsubsection*{解析一个彩色图像}

Netbpm 彩色图像格式仅仅比第十章所提到的灰度图像格式更复杂一点。文件头的识别字符串是“P6”，剩下的头文件与灰度格式一致。图像的文件体内，每个像素
都代表三个字节，它们是红，绿，蓝。

我们将图像数据表示为二维的像素数组。这里使用数组是为了更好的学习它。对于应用而言，当然可以使用列表的列表。这里使用数组的唯一优点就是：可以高效的
提取一行数据。

\begin{lstlisting}[language=Haskell]
  type Pixel = Word8

  type RGB = (Pixel, Pixel, Pixel)

  type Pixmap = Array (Int, Int) RGB
\end{lstlisting}

我们提供了一些类型同义词使得类型签名更可读。

由于 Haskell 在布局数组方面给予了相当大的自由，我们必须选择一种来表示。我们无需显式的存储图像的维度，因为可以使用\acode{bounds}函数来提取它们。

实际上解析器非常简短，感谢我们在第十章所开发的组合子。

\begin{lstlisting}[language=Haskell]
  parseRawPPM :: Parse Pixmap
  parseRawPPM =
    parseWhileWith w2c (/= '\n') ==> \header ->
      skipSpaces
        ==>& assert (header == "P6") "invalid raw header"
        ==>& parseNat
        ==> \width ->
          skipSpaces
            ==>& parseNat
            ==> \height ->
              skipSpaces
                ==>& parseNat
                ==> \maxValue ->
                  assert (maxValue == 255) "max value out of spec"
                    ==>& parseByte
                    ==>& parseTimes (width * height) parseRGB
                    ==> \pxs ->
                      identity (listArray ((0, 0), (width - 1, height - 1)) pxs)

  parseRGB :: Parse RGB
  parseRGB =
    parseByte ==> \r ->
      parseByte ==> \g ->
        parseByte ==> \b ->
          identity (r, g, b)

  parseTimes :: Int -> Parse a -> Parse [a]
  parseTimes 0 _ = identity []
  parseTimes n p = p ==> \x -> (x :) <$> parseTimes (n - 1) p
\end{lstlisting}

上述代码中仅需要注意的是\acode{parseTimes}，它调用其它解析器来解析一个给定的时间，再构建一个结果的列表。

\subsubsection*{灰度转化}

现在我们有了彩色图像的解析，我们需要将彩色数据转换成单色的。一个中间步骤就是转换数据为灰度。将 RGB 图像转换成灰度图像有一个广泛运用的公式，即基于
每个颜色通道的感知亮度。

\begin{lstlisting}[language=Haskell]
  luminance :: (Pixel, Pixel, Pixel) -> Pixel
  luminance (r, g, b) = round (r' * 0.30 + g' * 0.59 + b' * 0.11)
    where
      r' = fromIntegral r
      g' = fromIntegral g
      b' = fromIntegral b
\end{lstlisting}

Haskell 的数组是\acode{Functor} typeclass 的成员，所以我们可以使用\acode{fmap}来讲整个图像，或是单个扫描行，从彩色转为灰度。

\begin{lstlisting}[language=Haskell]
  type Greymap = Array (Int, Int) Pixel

  pixmapToGreymap :: Pixmap -> Greymap
  pixmapToGreymap = fmap luminance
\end{lstlisting}

\acode{pixmapToGreymap}函数仅用于解释。因为我们将仅检查一个条形码图像的某几行，没有必要做额外的工作来转换我们永远用不上的序列。

\subsubsection*{灰度至二进制以及类型安全}

下一个子问题是将灰度图像转换为一个二值图像，即每个像素要么是开要么是闭。

在一个图像处理应用中，需要处理大量的数字，因此很容易为了几个不同的目的重用相同的数字类型。例如使用\acode{Pixel}类型来表述 on/off 状态，使用转换将
数字一来代表“on”数字零“off”。

然而像这样为了多个目的而重用类型很快会带来潜在的混乱。为了判断一个“Pixel”是否为一个数值或是一个 on/off 值，我们不再可以简单的通过观察类型签名而知。
在某些上下文中，我们可以很容易使用包含“错误类型的数字”的值，并且编译器不会对其捕获，因为类型是正确的。

我们可以尝试引入类型别称来解决这个问题。同样的声明\acode{Pixel}作为\acode{Word8}的同义词，再使用\acode{Bit}类型作为\acode{Pixel}的同义词。
虽然这可以提高可读性，但是类型同义词仍然无法令编译器为我们做为何有效的检查。

编译器会将\acode{Pixel}与\acode{Bit}视为同样的类型，所以它不能捕获，例如在期望\acode{Bit}值为零或一这样的函数中使用\acode{Pixel}或是 253 值，
这样的异常。

如果我们自定义单色类型，编译器则会阻止我们不小心混合了其它的类型。

\begin{lstlisting}[language=Haskell]
  data Bit = Zero | One deriving (Eq, Show)

  threshold :: (Ix k, Integral a) => Double -> Array k a -> Array k Bit
  threshold n a = binary <$> a
    where
      binary i
        | i < pivot = Zero
        | otherwise = One
      pivot = round $ least + (greatest - least) * n
      least = fromIntegral $ choose (<) a
      greatest = fromIntegral $ choose (>) a
      choose f = foldA1 $ \x y -> if f x y then x else y
\end{lstlisting}

我们的\acode{threshold}函数计算了作为输入的数组的最大最小值。它接受这些值和一个介于 0 到 1 之间的阈值，并计算一个“枢轴”值。接着数组中的每个值，
如果其值小于枢轴值，返回\acode{Zero}，否则\acode{One}。

\subsection*{我们对图像做了什么}

略

\subsection*{寻找匹配的位数}

我们的第一个问题就是找到给定位置\textit{可能}被编码的数值。然后是做几个简化的假设：首先处理的是单行，其次确切知道条形码左边缘在一行中的起始位置。

\subsubsection*{运行长度编码}

我们该如何解决条形码粗细的问题呢？答案是运行图像数据的长度编码。

\begin{lstlisting}[language=Haskell]
  type Run = Int

  type RunLength a = [(Run, a)]

  runLength :: (Eq a) => [a] -> RunLength a
  runLength = map rle . group
    where
      rle xs = (length xs, head xs)
\end{lstlisting}

\acode{group}函数接受一个一系列 id 元素的列表，将它们分组成子列表。

\begin{lstlisting}[language=Haskell]
  ghci> group [1,1,2,3,3,3,3]
  [[1,1],[2],[3,3,3,3]]
\end{lstlisting}

\acode{runLength}函数将每组表示为其长度与第一个元素的二元组。

\begin{lstlisting}[language=Haskell]
  ghci> let bits = [0,0,1,1,0,0,1,1,0,0,0,0,0,0,1,1,1,1,0,0,0,0]
  ghci> runLength bits
  [(2,0),(2,1),(2,0),(2,1),(6,0),(4,1),(4,0)]
\end{lstlisting}

由于编码的数据都是一和零，我们可以将编码值扔了而不会失去任何有用的信息，即每次运行仅保留长度。

\begin{lstlisting}[language=Haskell]
  runLengths :: Eq a => [a] -> [Run]
  runLengths = map fst . runLength
\end{lstlisting}

\begin{lstlisting}[language=Haskell]
  ghci> runLengths bits
  [2,2,2,2,6,4,4]
\end{lstlisting}

上面的位模式不是随机的；它们是左外守卫也是我们捕获图像中每一行的第一个编码数字。如果丢弃守卫条形码，那么留下的长度为\acode{[2,6,4,4]}。

\subsubsection*{缩放运行长度并找到近似匹配}

一个可行的方法是缩放运行长度，使它们的和为一。我们将使用\acode{Ratio Int}类型而不是通常的\acode{Double}来管理这些缩放值，因为\acode{Ratio}在
\textbf{ghci}中有更可读的打印。这使调试和开发的交互变得更简单。

\begin{lstlisting}[language=Haskell]
  type Score = Ratio Int

  scaleToOne :: [Run] -> [Score]
  scaleToOne xs = map divide xs
    where
      divide d = fromIntegral d / divisor
      divisor = fromIntegral (sum xs)

  -- A more compact alternative that "knows" we're using Ratio Int:
  -- scaleToOne xs = map (% sum xs) xs

  type ScoreTable = [[Score]]

  -- "SRL" means "scaled run length"
  asSRL :: [String] -> ScoreTable
  asSRL = map (scaleToOne . runLengths)

  leftOddSRL = asSRL leftOddList

  leftEvenSRL = asSRL leftEvenList

  rightSRL = asSRL rightList

  paritySRL = asSRL parityList
\end{lstlisting}

这里使用\acode{Score}类型同义词可以让大多数代码不需要考虑其本身的类型是什么。

我们可以使用\acode{scaleToOne}来对正在搜寻的一系列数位进行缩放。我们现在纠正了距离而导致的条形码宽度变化，因为缩放的运行长度编码表中的条目与图像中
提取的运行长度序列之间应该有相等接近的匹配。

下一个问题是如何将直觉的“相当近”转为可测量的“足够近”。给定的两个缩放运行长度序列，我们可以计算它们之间的一个近似“距离”。

\begin{lstlisting}[language=Haskell]
  distance :: [Score] -> [Score] -> Score
  distance a b = sum . map abs $ zipWith (-) a b
\end{lstlisting}

完全匹配的距离会得到零，越弱的匹配则有越大的距离。

\begin{lstlisting}[language=Haskell]
  ghci> let group = scaleToOne [2,6,4,4]
  ghci> distance group (head leftEvenSRL)
  13 % 28
  ghci> distance group (head leftOddSRL)
  17 % 28
\end{lstlisting}

给定一个缩放后的运行长度表，选择表中最优的几个匹配用作输入序列。

\begin{lstlisting}[language=Haskell]
  type Digit = Word8

  bestScores :: ScoreTable -> [Run] -> [(Score, Digit)]
  bestScores srl ps = take 3 . sort $ scores
    where
      scores = zip [distance d (scaleToOne ps) | d <- srl] digits
      digits = [0 .. 9]
\end{lstlisting}

\subsubsection*{列表表达式}

之前的例子中提到了一个新的概念，即\textit{列表表达式}，根据一个或多个列表来创建一个列表。

\begin{lstlisting}[language=Haskell]
  ghci> [(a,b) | a <- [1,2], b <- "abc"]
  [(1,'a'),(1,'b'),(1,'c'),(2,'a'),(2,'b'),(2,'c')]
\end{lstlisting}

在竖线左侧的表达式会在右侧\textit{生成器表达式}的每个组合后计算。生成器表达式通过\acode{a <- }以及右侧列表中的一个元素进行变量的绑定。上述例子中，
生成器的组合子按照深度的顺序进行计算：首先是第一个列表的第一个元素，再计算第二个列表中的每个元素，以此类推。

另外关于生成器，我们还可以在列表表达式的右侧指定守护。一个守护就是一个\acode{Bool}表达式，如果结果为\acode{False}，那么元素被跳过。

\begin{lstlisting}[language=Haskell]
  ghci> [(a,b) | a <- [1..6], b <- [5..7], even (a + b ^ 2)]
  [(1,5),(1,7),(2,6),(3,5),(3,7),(4,6),(5,5),(5,7),(6,6)]
\end{lstlisting}

我们还可以用\acode{let}表达式来绑定本地变量。

\begin{lstlisting}[language=Haskell]
  ghci> let vowel = (`elem` "aeiou")
  ghci> [x | a <- "etaoin", b <- "shrdlu", let x = [a,b], all vowel x]
  ["eu","au","ou","iu"]
\end{lstlisting}

如果在一个生成器表达式中模式匹配失败了，不会有异常出现。相反，列表的元素被跳过。

\begin{lstlisting}[language=Haskell]
  ghci> [a | (3,a) <- [(1, 'y'),(3,'e'),(5,'p')]]
  "e"
\end{lstlisting}

列表表达式非常的强大已经简洁。然而它们不利于阅读。

\subsubsection*{记住匹配的奇偶}

对于左侧组的每个匹配，我们需要记住其是否在偶数表格还是奇数表格。

\begin{lstlisting}[language=Haskell]
  data Parity a = Even a | Odd a | None a deriving (Show)

  fromParity :: Parity a -> a
  fromParity (Even a) = a
  fromParity (Odd a) = a
  fromParity (None a) = a

  parityMap :: (a -> b) -> Parity a -> Parity b
  parityMap f (Even a) = Even (f a)
  parityMap f (Odd a) = Odd (f a)
  parityMap f (None a) = None (f a)

  instance Functor Parity where
    fmap = parityMap
\end{lstlisting}

我们将一个编码好的值包装进 parity 内，并为其实现\acode{Functor}实例，这样就可以轻松的操控 parity 内部的值了。

我们希望根据 parity 所包含的值进行排序。\acode{Data.Function}模块为此还提供了一个方便的组合子，\acode{on}。

\begin{lstlisting}[language=Haskell]
  import Data.Function (on)

  -- on :: (a -> a -> b) -> (c -> a) -> c -> c -> b
  -- on f g x y = g x `f` g y

  compareWithoutParity :: Parity (Score, Digit) -> Parity (Score, Digit) -> Ordering
  compareWithoutParity = compare `on` fromParity
\end{lstlisting}

\textbf{注}：文件头引入了\acode{Data.Function}的\acode{on}，另外显式标注了\acode{compareWithoutParity}的函数签名。

为了避免理解不清楚，尝试将\acode{on}想象为一个带有两个参数的函数，\acode{f}与\acode{g}，该函数返回另一个带两个参数的函数，\acode{x}与\acode{y}。
然后将\acode{g}分别应用至\acode{x}与\acode{y}，再将\acode{f}应用至上述两个结果。

将匹配包装进一个 parity 值很直接。

\begin{lstlisting}[language=Haskell]
  bestLeft :: [Run] -> [Parity (Score, Digit)]
  bestLeft ps =
    sortBy compareWithoutParity $
      map Odd (bestScores leftOddSRL ps) ++ map Even (bestScores leftEvenSRL ps)

  bestRight :: [Run] -> [Parity (Score, Digit)]
  bestRight = map None . bestScores rightSRL
\end{lstlisting}

有了奇偶表格的最佳左侧匹配后，可以仅基于每个匹配的质量对它们进行排序

\subsubsection*{另一种惰性，键盘上的}

\acode{Parity}类型可以使用 Haskell 的 record 语法来避免编写\acode{fromParity}函数。换言之，可以这么写：

\begin{lstlisting}[language=Haskell]
  data AltParity a
    = AltEven {fromAltParity :: a}
    | AltOdd {fromAltParity :: a}
    | AltNone {fromAltParity :: a}
    deriving (Show)
\end{lstlisting}

那么为什么不这么做呢？答案有点令人羞愧，这与\textbf{ghci}中的交互式调试有关。当我们告诉 GHC 自动派生类型的\acode{Show}实例时，它会根据是否使用记录
语法声明该类型而生成不同的代码。

\begin{lstlisting}[language=Haskell]
  ghci> show $ Even 1
  "Even 1"
  ghci> show $ AltEven 1
  "AltEven {fromAltParity = 1}"
  ghci> length . show $ Even 1
  6
  ghci> length . show $ AltEven 1
  27
\end{lstlisting}

\acode{Show}实例对于使用 record 语义而言显然更加冗长。

\subsubsection*{列表切分}

处理列表的通常做法就是对它们进行切分。例如，条形码中的每个位数是由四个位数所编码的。我们可以将平整的列表表示为四个元素所构成的列表的列表。

\begin{lstlisting}[language=Haskell]
  chuckWith :: ([a] -> ([a], [a])) -> [a] -> [[a]]
  chuckWith _ [] = []
  chuckWith f xs = let (h, t) = f xs in h : chuckWith f t

  chunksOf :: Int -> [a] -> [[a]]
  chunksOf n = chuckWith $ splitAt n
\end{lstlisting}

\subsubsection*{生成候选位数列表}

有了这些帮助函数后，编写一个用于生成每个数位组的候选匹配列表函数就很简单了。首先需要提前检查匹配偶数是否合理。所有的运行都需要以黑（\acode{Zero}）
条码开始，并包含足够的条码。

\begin{lstlisting}[language=Haskell]
  candidateDigits :: RunLength Bit -> [[Parity Digit]]
  candidateDigits ((_, One) : _) = []
  candidateDigits rle | length rle < 59 = []
\end{lstlisting}

对于\acode{bestLeft}或\acode{bestRight}返回的空列表，并不能被匹配。否则返回一个由校验编码的候选数字列表组成的列表。外部列表由十二个元素构成，
条形码的每个数位有一个元素。每个子列表中的数字按匹配质量排序。

\begin{lstlisting}[language=Haskell]
  candidateDigits rle
    | any null match = []
    | otherwise = map (map (fmap snd)) match
    where
      match = map bestLeft left ++ map bestRight right
      left = chunksOf 4 . take 24 . drop 3 $ runLengths
      right = chunksOf 4 . take 24 . drop 32 $ runLengths
      runLengths = map fst rle
\end{lstlisting}

测试：

\begin{lstlisting}[language=Haskell]
  ghci> let input = zip (runLengths $ encodeEAN13 "978013211467") (cycle [Zero, One])
  ghci> :t input
  input :: [(Run, Bit)]
  ghci> take 7 input
  [(1,Zero),(1,One),(1,Zero),(1,One),(3,Zero),(1,One),(2,Zero)]
  ghci> mapM_ print $ candidateDigits input
  [Odd 7,Even 1,Even 2,Odd 3,Even 4,Odd 8]
  [Even 8,Odd 0,Odd 1,Odd 2,Even 6,Even 7]
  [Even 0,Even 1,Odd 2,Odd 4,Odd 6,Even 9]
  [Odd 1,Odd 0,Even 1,Odd 2,Even 2,Even 4]
  [Even 3,Odd 4,Odd 5,Even 7,Even 0,Odd 1]
  [Odd 1,Even 1,Odd 2,Even 2,Odd 0,Even 0]
  [None 1,None 0,None 2]
  [None 1,None 0,None 2]
  [None 4,None 2,None 5]
  [None 6,None 8,None 2]
  [None 7,None 3,None 8]
  [None 7,None 3,None 8]
\end{lstlisting}

\textbf{注}：原文缺失：\acode{let input = ...}。

\subsection*{没有数组或哈希表的世界}

在命令式语言中，数组就跟 Haskell 的列表和元组那样，作为核心。在命令式语言中数组通常是可变的。

而 Haskell 中的数组是\textit{不}可变的。这就意味着“修改”数组中的一个元素，整个数组会被拷贝，同时将那个元素设为新值。很明显，这样性能很差。

可变数组是由另一个常用的命令式数据结构所构建的，即哈希表。经典的实现中，数组像是在“旋转”表，每个元素包含了一个列表。为哈希表添加一个元素时，将
哈希元素来找到列表的偏移量，并在该处修改列表并添加元素。

如果数组不是可变的，更新一个哈希表则需要创建一个新的。拷贝数组，将一个新的列表放在偏移处。我们无需拷贝其它偏移处的列表，但是我们已经拷贝了整个
哈希表导致了更差的性能。

不可变数组从我们的工具箱中消除了两个规范的命令式数据结构。相较于很多其它语言，在纯 Haskell 中数组显得没有这么有用。不过很多数组代码只会在构建时
更新，然后以只读的方式使用它。

\subsubsection*{树作为解决方案}

尽管情况看上去好像并没有那么不好。数组和哈希表通常作为由键作为索引的集合，而在 Haskell 中我们则使用\textit{树}。

在 Haskell 实现一个简单的树类型非常的容易。除此之外，更有用的树类型也非常容易实现。自平衡结构，如红黑树，其平衡算法是出奇的难以被正确的使用。

Haskell 的代数数据类型组合，模式匹配，以及守护，将最复杂的平衡操作简化成了几行代码。

对于函数式程序员而言树的魅力则是其\textit{廉价的修改}。我们不会打破不变的规则：树是不变的。当需要修改一个树时，则创建一个新树，这一过程可以将
新旧版本的树中绝大多数的结构进行共享。例如一个树包含了 10,000 个节点，那么在添加或删除一个元素时，可以预期新旧版本共享了 9,985 个元素。换言之，
每次更新需要改变元素的数量是取决于树的高度，或者树大小的对数。

Haskell 标准库提供了两种由平衡树所实现的集合类型：\acode{Data.Map}用于键值对，\acode{Data.Set}用于值集合。

\begin{anote}
  关于性能

  相较于哈希表，一个实现良好的存函数树数据结构将具有竞争力。因此不应该假设代码在处理树时会损失性能。
\end{anote}

\subsubsection*{maps 的简介}

\acode{Data.Map}模块提供了一个参数化类型\acode{Map k a}。尽管其内部是一个大小平衡的二元树，其实现并不对我们可见。

\acode{Map}的键是严格的 strict，但是值确实非严格的 non-strict。换言之，map 的结构总是维持更新状态，但是值只会在使用时才会被计算。

记住这点很重要，因为\acode{Map}在于值的惰性是导致空间泄漏的常见来源。

由于\acode{Data.Map}模块包含了一系列与 Prelude 重名的名称，通常需要通过 qualified 的方式进行引入。

\subsubsection*{类型约束}

\acode{Map}类型再起键类型上并没有任何显式的约束，但是模块中大多数有用的函数都需要键是\acode{Ord}的实例。这是值得注意的，因为这是 Haskell 代码中
常见的设计模式：类型约束只会在真实需要时才进行声明。

\acode{Map}类型和模块中的任何函数都不会约束用作值的类型。

\subsubsection*{部分应用程序的笨拙}

由于某些原因，\acode{Data.Map}中的函数类型签名对部分应用 partial application 而言并不友好。map 的参数总是最后出现，毕竟先出现对于部分应用会更为
友好。因此，使用部分应用的映射函数的代码几乎总是包含适配器函数来调整参数顺序。

\subsubsection*{从 API 开始}

\subsection*{将位数变为答案}

\subsubsection*{并行的校验数}

\subsubsection*{通过第一位数完成求解 map}

\subsubsection*{寻找正确的序列}

\subsection*{处理行数据}

\subsection*{合并所有}

\subsection*{一些开发风格的评论}

\end{document}
